!% +language_name=Swedish
!*******************************************************************************
!** Uppvaknandet
!** en översättning av The Awakening - Copyright (c) 1998 av Dennis Matheson
!**
!** Omarbetad till svenskan av Björn Paulsen 2020
!** Med tack till Fredrik Ramsberg
!**
!** Nedanstående text är reproducerad från den ursprungliga källkoden. Alla
!** legala riktlinjer följer urpsprungsverket.
!**
!** Distribution: ~The Awakening~ may be distributed freely by any means
!** so long as a) This notice remains intact and b) no charge other than a nominal
!** fee to cover the cost of distribution is made. It may be included as part of
!** a commercial distribution provided that my permission is given first.
!**
!** Derivative Works: ~The Awakening~ is distributed with its source code.
!** Derivative works are permitted of ~The Awakening~ so long as a) They are clearly
!** identified as such and reference is made back to this original version and b)
!** such works must freely redistributable under the same terms as stated above.
!**
!** Support: ~The Awakening~ is distributed without any support of any
!** kind. Feel free to contact me at the address given, but I make no guarantee that
!** I will respond.
!**
!** Warranty: ~The Awakening~ is distributed totally without any warranty of any
!** kind. The entire risk of using this program is totally assumed by the user.
!**
!** As of July 1998
!** Dennis Matheson
!** Dennis_Matheson@compuserve.com
!**
!** 4068 Maxanne Dr.
!** Kennesaw, GA  30144
!**
!*******************************************************************************

Replace _CheckThisObj;
Include "SweAlpha";

! Fixar så att Örnkoll inte bryr sig om meny-objekt.
[_CheckThisObj obj   j isNormalObj;
  isNormalObj=(parent(obj)~=0 || obj hasnt light) && ~~( obj ofclass HintTopic || obj ofclass SwitchOption || obj ofclass Option || obj ofclass Hint || obj ofclass Menu);
  if(isNormalObj && parent(obj)==0) { ! Check if dark, lonely objects have exits
    objectloop(j in compass) isNormalObj=isNormalObj && (obj.(j.door_dir)==0);
  }
  return isNormalObj;
];

Constant Story "Uppvaknandet";
Constant Headline "^Copyright (c) 1998 av Dennis Matheson^
                   Svensk tolkning av Björn Paulsen (v1.5 alfa)^
                   (För vidare information, skriv ~HJÄLP~. För ledtrådar, skriv ~LEDTRÅDAR~)^^";

Constant DEATH_MENTION_UNDO;
Constant TASKS_PROVIDED;

Global hint_menu = Hint_Top;
Global help_menu = Help_Top;

Replace DoMenu;
Replace LowKey_Menu;

Include "Parser";
Include "Utility";

Constant NUMBER_TASKS = 7;
Array    task_scores  -> 10 5 5 5 10 5 10;
Constant MAX_SCORE = 50;

Object LibraryMessages
   with before [;
      Miscellany:
         if (lm_n == 19)
            "Du finner dig oförmögen att se ner på dig själv. Det är nästan som om du
            vet vad du skulle kunna finna, och är rädd för att få dina farhågor bekräftade.";
      Climb:
         if (location == Grave)
              <<Go u_obj>>;];

Include "VerbLib";

Include "SweDoMenu";

Constant ALTMENU_PROP_STUBS;

Include "AltMenu";

Verb meta "hjälp" "om"
     *               -> Help;

[HelpSub;
   help_menu.select();];

Include "SweRout";
Include "SweHints";

!*******************************************************************************
!*** Categorization
!*******************************************************************************
Constant CAT_DISTANT = 1;
Constant CAT_IMMATERIAL = 2;
Constant CAT_DETRITUS = 4;
Constant CAT_SOLID = 8;
Constant CAT_AMORPHOUS = 16;

[ GetCategory o;
  switch (o) {
     Nonfunctional_Gate, Wrought_Iron_Fence, Graves, Branch, Disturbed_Grave: return CAT_SOLID;
     Rain, Outside_Storm: return CAT_IMMATERIAL;
     Steeple, Church, Windows, Distant_Choir, Distant_Eyes, Painting: return CAT_DISTANT;
     Chain: return CAT_AMORPHOUS;
     default: return CAT_DETRITUS;
  }
];

!*******************************************************************************
!*** Class Descriptions
!*******************************************************************************
Class Fake_Object
   with before [;
      Knock:  "", (The) self, " låter sig inte knackas härifrån.";
   ],
   has static;

Class Bottle
   with
      colour "färglöst",
   name
      'tung' 'glas' 'flaska' 'glasflaska' 'blyglas' 'propp' 'kork' 'igenkorkad',
   description [;
      "Flaskan är i tungt blyglas och plomberad med kork och vax. Inuti flaskan hänger
      ett underligt format lod av bly likt en pendel på ett snöre. Flaskan glöder med
      ett ",
      (string) self.colour, " sken.";],
   before [;
      Take:
         move self to player;
         if (self hasnt general) {
            move Bottles to Limbo;
            give Blue_Bottle ~concealed;
            give Green_Bottle ~concealed;}
         if (self == Blue_Bottle) {
            if (self hasnt general) {
               give self general;
               "Den blå flaskan känns varm mot huden när du griper den. Värmen tycks
               färdas uppför din arm och tränga in djupt inuti dig. Det är nästan
               som om du återfått en del av dig själv du saknat.";}
            else
               "Du dröjer kvar vid känslan av värme från den blå flaskan då du griper
               tag i den.";}
         else {
            if (self hasnt general) {
               give self general;
               "Den gröna flaskan ger ett sjukligt intryck som passar ihop med färgen.
               Den känns som is, både kylig och brännhet på en och samma gång. Du lyckas
               nätt och jämnt undvika att kasta ifrån dig den."; }
            else
               "Du tar motvilligt den gröna flaskan igen.";}
      ThrownAt:
         move noun to location;
         print (The) noun, " splittrar ", (the) self, ".  ";
         self.open_bottle();
         rtrue;
      Open:
         print "Du skalar av vaxet från flaskans mynning och drar ut korken. ";
         self.open_bottle();
         rtrue;
      Attack:
         print "Du krossar ", (the) self, ".  ";
         self.open_bottle();
         rtrue;],
   has light;

!*******************************************************************************
!*** Hidden Game Locations and Things That Don't Start Anywhere
!*******************************************************************************
Object Limbo "Limbo" has ~light;

!*******************************************************************************
!*** The start
!*******************************************************************************
Object Grave "I gyttjan"
   with description
      "Regn och lervälling översköljer dig och du kämpar för att se eller
      ens andas. Fuktig jord och lera omgärdar dig på alla sidor förutom ovan,
      där en stormpiskad himmel fläckvis avtecknar sig i skenet av blixtar.",
   before [;
   Listen:
         "Vindens vrål och åskans dunder dränker dina sinnen.";],
   u_to [;print "Medan du kämpar dig upp up gropen ger gyttjan efter och viker sig
      under dig, och hålet bakom dig fylls igen.^"; return Graveyard;],
   out_to [; <<Go u_obj>>; ],
   cant_go "Du sliter hjälplöst i gyttjan.",
   has light;

Object Muddy_Water "dyigt vatten" Grave
   with name
      'gyttja' 'gyttjan' 'dy' 'dyn' 'dyigt' 'dyiga' 'lerigt' 'leriga' 'vatten' 'vattnet',
   short_name_indef "vatten",
   short_name_def "vattnet",
   description
      "Mängder av dyigt vatten öser ner över dig genom öppningen därovan.",
   before [;
      Take:
         "Det dyiga vattnet sipprar mellan dina fingrar.";],
   has scenery uter;

Object Mud Grave
   with name
      'grop' 'gropen' 'hål' 'hålet' 'lera' 'leran' 'lerig' 'leriga' 'jord' 'jorden' 'fuktig' 'fuktiga' 'mustig' 'mustiga' 'mörk' 'mörka' 'mylla' 'myllan',
   short_name_indef "grop",
   short_name_def "gropen",
   description
      "Gropens sidor består av mörk, mustig jord, nu genomvåt av det kraftiga
      regnet. Medan du ser på spolas mer och mer lera loss och rinner ner i gropen
      där du står.",
   before [;
      Climb:
          <<Go u_obj>>;
      Take:
         "Du tar en näve gyttjig lera, men regnet sköljer bort den.";],
   has scenery uter;

Class Area with
    u_to "Ännu är tiden inte mogen...",
    d_to "Du hör inte längre hemma där.",
    has light;

!*******************************************************************************
!*** The outside
!*******************************************************************************
Class Outside class Area
   with
   before [;
      Smell:
         "Man kunde tro att stormen skulle ha rensat luften, men en unken doft av
         röta vilar över hela området.";],
   cant_go "Du känner dig oförmögen att röra dig dit. Någon slags kraft tilltar
   när du försöker lämna platsen.",
   each_turn [;
      if (random(5) == 1) {
         switch (random(5)) {
            1: "^En ensam åskknall får marken att skaka.";
            2: print "^Det hörs ett öronbedövande "; style bold; print "KRACK"; style roman; " då en blixt slår ner i kyrktornet.";
            3: "^Vinden och regnet mojnar för ett ögonblick, bara för att tillta med
               fördubblad styrka.";
            4: "^Blixten ljungar ovanför ditt huvud, och åskans dån följer omedelbart
               därpå.";
            5: "^Under ett kort ögonblick smattrar en hagelskur ner och bedarrar sedan.";}}];

Outside Graveyard "Kyrkogård"
   with description
      "Ett dussintal minnesmärken står här, oberörda av vinden och regnet. Runt om dem
      reser sig ekar, väldiga och förvridna: vinden sliter och piskar deras grenar
      medan knotiga rötter suger en onämnbar näring ur den mossbemängda, mögelansatta
      lerjorden. Över hela platsen vilar en känsla av förruttnelse som inte ens stormen
      förmår skingra. Ett järnsmitt stängsel omsluter kyrkogården, en av klätterväxter
      bemängd barriär mellan den och de levandes värld.",
   before [;
      Smell:
         "Du kan känna lukten av fuktig jord, stormens kittlande dofter, och under allt en
         kvardröjande, sötaktig dunst av ruttnande kött.";],
   e_to West_Church,
   cant_go "Kyrkogården omsluts på alla sidor av ett järnsmitt stängsel. Enda utgången är
      genom porten åt öster.";

! Lathund för parsing. Använd i parse_name för att lägga till dessa som synonymer.
[Adjective_Old wd; return (Adjective_Aged(wd) || Adjective_Ancient(wd));];
[Adjective_Aged wd;  return (wd == 'gammal' or 'gamla' or 'gamle' or 'gammalt' or 'åldrad' or 'åldrade' or 'åldrat' ); ];
[Adjective_Ancient wd; return (wd == 'urgammal' or 'urgammalt' or 'urgamla' or 'uråldrig' or 'uråldriga');];
[Adjective_Shattered wd; return (wd == 'splittrad' or 'splittrade' or 'sprucken' or 'spruckna' or 'förstörd' or 'förstörda'
          or 'sönder' or 'bruten' or 'brutna' or 'söndrig' or 'söndriga' or 'förstörd' or 'förstörda' or 'sönderslagen' );];
[Adjective_Broken_Off wd; return (wd == 'bruten' or 'avbruten' or 'brutna' or 'brutet' or 'avbrutna' or 'avbrutet' or 'knäckt' or 'knäckta');];
[Noun_Fence wd; return (wd == 'staket' or 'staketet' or 'stängsel' or 'stängslet');];
[Noun_Wall wd; return (Noun_Fence(wd) || wd == 'barriär' || wd == 'barriären' || wd == 'mur' || wd == 'muren');];

Object Nonfunctional_Gate
   with name 'grind' 'grinden' 'port' 'porten' 'dörr' 'dörren',
   short_name_indef [; if (location == Graveyard or West_Church) print "grind"; else print "port"; rtrue; ],
   short_name_def [; if (location == Graveyard or West_Church) print "kyrkogårdens grind"; else print "kyrkans port"; rtrue; ],
   description [; if (location == Graveyard or West_Church)
           "En grind av järn som hänger på trekvart. En gång i tiden var den förmodligen en ståtlig syn. Nu har ytan flagnat och rosten letat sig in i sprickorna likt sårfeber.";
        "En port som leder in i kyrkan. Den står på glänt, men inget ljus syns innanför.";
   ],
   before [;
      Open: print (The) self; " står redan på glänt.";
      Close: "Du känner att du bör lämna ", (the) self, " öppen.";
      Enter:
        switch (location) {
          Graveyard: <<Go e_obj>>;
          West_Church: <<Go w_obj>>;
          Front_Church: <<Go n_obj>>;
          Vestibule: <<Go s_obj>>;
        };
   ],
   found_in Graveyard West_Church Porch Vestibule,
   has scenery uter;


Object Wrought_Iron_Fence
   with name
      'järnsmitt' 'järnsmidda' 'smidesjärn' 'metall' 'av' 'järn' 'järnstängsel' 'rostiga' 'rostigt',
   parse_name [ wd noun_count;
        if (parser_action == ##TheSame) return -2;
        wd = NextWord();
        while (wd ~= 0) {
          if (WordInProperty(wd, self, name) || Noun_Wall (wd)) {
            noun_count++;
          } else {
            return 0;
          }
          wd = NextWord();
        }
        return noun_count;
        ],
   short_name_indef "järnsmitt stängsel",
   short_name_def "järnsmidda stängslet",
   description [; print "Ett högt, spetsigt stängsel av smidesjärn. Rostigt järn
      blänker i blixtens sken, men merparten av staketet är övervuxet av klätterväxter. Den
      enda öppningen i det är en port som leder ";
      if (location == Graveyard) "öst.";
      "västerut, tillbaks till kyrkogården.";
    ],
   before [;
      Take, Push, Pull, Attack: "Stängslet är orubbligt.";
      Climb: "Fotfästet är för halt och spetsarna på toppen olycksbådande vassa.";
   ],
   found_in Graveyard West_Church,
   has static scenery neuter def_article;


Object Graves "minnesmärken" Graveyard
   with name
      'minnesmärke' 'minnesmärkena' 'gravar' 'gravarna' 'gravstenar' 'gravstenarna' 'sten' 'stenar' 'stenarna' 'kyrkogård' 'kyrkogården',
   parse_name [ wd adj_count noun_count;
        if (parser_action == ##TheSame) return -2;
        wd = NextWord();
        while (wd ~= 0) {
          if (Adjective_Old(wd)) {
            adj_count++;
          } else if (WordInProperty(wd, self, name)) {
            noun_count++;
          } else {
            return 0;
          }
          wd = NextWord();
        }
        if (noun_count > 0) return noun_count + adj_count;
        return 0;
        ],
   short_name_indef "minnesmärken",
   short_name_def "minnesmärkena",
   description
      "Dussinet minnesmärken står resta här över de bortgångnas sista viloplats.
      De är urgamla, deras inskriptioner är nötta och täckta med ett lager
      mossa och mögel, de lutar i märkliga vinklar. Många har kantrat och ligger
      nu halvt begravna i leran.",
   before [;
      Read:
         "Få av inskriptionerna går att uttyda. De återstående består av visdomsord
         riktade till de som kan komma att läsa, snarare än de som vilar därunder.";
      Push, Pull:
         "Stenen välter över ända ner i gyttjan.";
      Take:
         "Du kämpar och lyfter stenen en aning ur gyttjan, inann den slinker ur ditt grepp
         och faller med ett plask.";],
   has static scenery pluralname;

[Branchmsg;
    if ((Limb hasnt moved) && (Branch has general)) {
         move Limb to Graveyard;
         " En mindre gren har brutits av från trädgrenen och ligger i närheten.";
       }
      else
         new_line;rtrue;
];

Object Branch "trädgren" Graveyard
   with name
      'tung' 'tunga' 'gren' 'grenen' 'trädgren' 'trädgrenen',
   short_name_indef "tung trädgren",
   short_name_def "tunga trädgrenen",
   description [;
      print "Denna tjocka gren har knäckts av från en av ekarna har slagit ner på en av gravarna,
      vars minnessten nu är sprucken.";
      return BranchMsg();],
   describe [;
      print "^En tjock gren som knäckts av från en av ekarna har spräckt gravstenen under sig.";
      return BranchMsg();],
   before [;
      Take:
         print "Grenen är för tung för att lyfta";
         if (Limb hasnt moved) {
            give self general;
            move Limb to Graveyard;
            ", men en mindre gren har brutits av och ligger i närheten."; }
         ".";
      Push, Pull:
         "Du knuffar på grenen utan något vidare resultat.";],
   has static uter def_article;

Object Limb "avbruten trädgren" Limbo
   with name
      'gren' 'grenen' 'liten' 'lilla' 'mindre' 'trädgren' 'trädgrenen' 'kvist' 'kvisten',
   parse_name [ wd adj_count noun_count;
        if (parser_action == ##TheSame) return -2;
        wd = NextWord();
        while (wd ~= 0) {
          if (Adjective_Broken_Off(wd)) {
            adj_count++;
          } else if (WordInProperty(wd, self, name)) {
            noun_count++;
          } else {
            return 0;
          }
          wd = NextWord();
        }
        if (noun_count > 0) return noun_count + adj_count;
        return 0;
        ],
   short_name_indef "avbruten gren",
   short_name_def "avbrutna grenen",
   description
      "En stadig gren från en ek, ungefär lika lång som du själv.",
   describe [;
     if (self hasnt moved) {
        rtrue;
     }
   ],
   has uter def_article;

Object Disturbed_Grave "uppsliten grav" Graveyard
   with name
      'grav' 'graven' 'gravsten' 'gravstenen' 'minnessten' 'minnesstenen' 'öppnade' 'öppnad' 'uppriven' 'upprivna' 'uppsliten' 'uppslitna' 'uppgrävd' 'uppgrävda' 'låg' 'låga' 'grop' 'gropen',
   parse_name [ wd adj_count noun_count;
        if (parser_action == ##TheSame) return -2;
        wd = NextWord();
        while (wd ~= 0) {
          if (Adjective_Old(wd) || Adjective_Shattered(wd)) {
            adj_count++;
          } else if (WordInProperty(wd, self, name)) {
            noun_count++;
          } else {
            return 0;
          }
          wd = NextWord();
        }
        if (noun_count > 0) return noun_count + adj_count;
        return 0;
        ],
   short_name_indef "grav",
   short_name_def "graven",
   description
      "Den här gravens minnessten ligger sönderslagen under en grov gren som fallit ned från trädet ovanför.
      Graven själv är inte längre intakt, marken har kollapsat och gett upphov till en låg grop.",
   before [;
      Take:
         "Det finns ingen anledning att samla ihop skärvorna av gravstenen.";
      Read:
         "Skriften går inte längre att tyda.";
      Enter:
         "Där väntar ingen vila.";],
   has scenery uter;


Object Moss_Mold "mögel och mossa" Graveyard
   with name
      'mossa' 'mögel' 'och' 'mossan' 'möglet',
   short_name_indef "mögel och mossa",
   short_name_def "möglet och mossan",
   description
      "Mossa och mögel täcker marken och gravstenarna. Det finns något osunt över dem,
      som vore de förkroppsligandet av någon slags gift i jordmånen. Oavsett ursprung
      växer de enväldigt på kyrkogården, där inget gräs eller ogräs kan gro.",
   before [;
      Take:
         "Du sliter loss en näve mossa och granskar den innan du låter den falla.
         Du ser ingen anledning att bära med dig det.";
      Burn:
         "Mossan och möglet är för fuktigt för att bränna.";],
   has scenery neuter;

[NoEntry;
  "Det finns ingen ingång till kyrkan från den här sidan.";
];

Outside West_Church "Kyrkans västra sida"
   with description
      "Den här sidan av kyrkan är oskött och gräs och sly har fått växa fritt.
      Ett lerigt spår leder tillbaks mot kyrkogården i väst och vidare mot sydöst, och
      en knappt märkbar stig fortsätter runt kyrkan åt nordöst.",
   w_to Graveyard,
   e_to [; return NoEntry(); ],
   ne_to North_Church,
   n_to North_Church,
   se_to Front_Church,
   s_to Front_Church;

Object Trail "lerigt spår" West_Church
   with name
      'lerig' 'lerig' 'leriga' 'tunn' 'tunna' 'spår' 'spåret' 'stig' 'stigen' 'sliten' 'slitna' 'suddig' 'suddiga' 'svag' 'svaga',
   short_name_indef "spår",
   short_name_def "spåret",
   description
      "Blott ett tunt spår i gräset som löper runt kyrkan åt nordöst och sydöst, och västerut mot kyrkogården till.",
   has scenery neuter;

Outside North_Church "Kyrkans norra sida"
   with description
      "Trädens stammar tränger på här, och deras stormpiskade grenar tycks
      sträcka sig klolikt mot kyrkans baksida. Ogräs florerar här och murgrönanS
      stretar envist uppåt längs kyrkans bakre vägg. Antydan till en stig fortsätter runt
      kyrkan åt sydöst och sydväst.",
   se_to East_Church,
   e_to East_Church,
   sw_to West_Church,
   w_to West_Church,
   s_to [; return NoEntry(); ];

Object Path "tunn stig" North_Church
   with name
      'tunn' 'tunt' 'försänkning' 'spår' 'spåret' 'väg' 'vägen' 'stig' 'stigen',
   short_name_indef "stig",
   short_name_def "stigen",
   description
      "En knappt märkbar försänkning i gräset som leder runt kyrkan åt sydöst och sydväst.",
   has scenery uter;

Outside East_Church "Kyrkans östra sida"
   with description
      "En gång i tiden kan denna vida, grusbetäckta ytan ha varit där församligen
      parkerade sina fordon under gudstjänsterna i kyrkan. Om så är fallet lär den
      sista mässan ha hållits för länge sedan. Gräs och ogräs pressar upp genom
      grusbeläggningen, och överallt ligger bråte och skräp. Mörka, ruvande träd omgärdar
      gläntan på alla sidor förutom mot infarten, som leder åt sydväst, och en trång
      öppning åt nordväst. ",
   sw_to Front_Church,
   s_to Front_Church,
   nw_to North_Church,
   n_to North_Church,
   w_to [; return NoEntry(); ];

Object Parking_Lot "grusig glänta" East_Church
   with name
      'singel' 'lera' 'grus' 'grusig' 'grusiga' 'glänta' 'gläntan' 'parkering' 'parkeringen' 'gruslager' 'gruslagret' 'grusplan' 'grusplanen',
   short_name_indef "glänta",
   short_name_def "gläntan",
   description
      "Smutsigt singel blandat med lera till en mer eller mindre plan yta.",
   before [;
      Take:
         if (Gravel notin player) {
            move Gravel to player;
            "Du fyller näven med grus.";}
         else
            "Du har redan näven full med grus.";
      Dig:
         "Du gräver en liten grop i gruset och ser hur stormen omedelbart fyller igen
         den med regn och lera.";],
   has scenery uter;

Object Gravel "näve grus" Limbo
   with name
      'handfull' 'näve' 'näven' 'med' 'grus' 'gruset',
   short_name_indef "grus",
   short_name_def "gruset",
   description
      "En näve löst, lerigt grus.",
   before [;
      Drop:
         move self to Limbo;
         print "Du kastar ifrån dig gruset och det sprider sig bland ";
         if (location ofclass Outside)
            "gräset.";
         else
            "allt skräp på golvet.";],
   after [;
      ThrowAt:
         move self to Limbo;
         "Gruset sprids ut över marken och försvinner.";],
   has uter;

Outside Front_Church "Kyrkans framsida"
   with description
      "Två massiva ekar flankerar den spruckna trottoaren och dominerar kyrkans framsida.
      Kyrkan själv reser sig bortom dem, med kyrktornet i silhuett mot himlen.
      Stormen piskar dess flagnande fasad, nästan som om ovädret försöker tvätta den ren.
      Inom vet du att något sådant skulle kräva långt mer än vatten. Åt norr leder en
      trottoar mot kyrkans port. En snårig uppfart går mot nordost och ned för en sluttning
      i söder, medan en lerig stig banar väg runt kyrkan åt nordväst.",
   e_to  East_Church,
   ne_to East_Church,
   w_to  West_Church,
   nw_to West_Church,
   in_to Porch,
   n_to [;if (Dog hasnt general)
             "Hunden står i vägen för dig, morrande och med raggen rest";
          else {
             Achieved(0);
             return Porch;}],
   u_to [;if (Railing_Section.is_under ~= Top_Of_Tree)
             "Trädstammarna är för slipprigt fuktiga för att klättras.";
          else
             return Railing_Section;];

Object Dog "vakthund" Front_Church
   with
      is_mad false,
      is_following false,
      temper 0,
      chain_length 0,
   short_name_indef "hund",
   short_name_def "hunden",
   name
      'hund' 'hunden' 'vakthund' 'vakthunden' 'svart' 'svarta' 'grov' 'grova' 'storväxt' 'storväxta' 'fido' 'rover' 'vovve' 'vovven',
   description [;
      if (self.is_mad)
         "Hunden saglar och skäller vildsint medan den slungar sig mot dig igen
         och igen.";
      else
         "En grov och storväxt svart hund som är kedjad vid en av ekarna. Något
         makabert vilar över formen på dess huvud - det tycks alltför stort
         och aningen missbildat, och en kuslig medvetenhet skymtar i djupet
         av dess röda ögon.";],
   describe
      "^En skräckinjagande vakthund står här.",
   orders [;
      "Hunden morrar dovt.";],
   before [;
      Touch, Rub, Kiss, Knock:
         "Hunden gläfser och hugger ut mot din utsträckta hand och du drar snabbt
         tillbaka den.";],
   life [;
      Attack:
         self.is_mad = true;
         "Hunden kastar sig efter dig i raseri över din attack!";
      ThrownAt:
         move noun to location;
         <<Attack self>>;
      default:
         "Hunden morrar och skäller åt dig.";],
   daemon [d p x;
      d = parent(self);
      p = location;
      if (p == d) {
         give self ~general;
         self.is_following = true;
         if (self.is_mad) {
            switch(self.temper++) {
               0: "^Hunden gläfser och morrar ilsket, och nafsar efter dina ben.";
               1: "^Hunden kastar sig mot dig och klöser mot dig med sina klor,
                  och river upp ett rött spår över ditt bröst.";
               2: "^En blixt av intensiv smärta flammar upp när hundens käkar
                  sargar dig i sidan.";
               3: deadflag = 1;
                  "^Djurets käkar slår med ens runt din strupe. Det sista du ser är
                  rännilar av ditt eget blod stänka över pälsen.";}}
         "^Hunden morrar hotfullt åt dig.";}
      else {
         if (self.temper > 0) self.temper--;
         if (self.is_following) {
            if ((self.is_mad) ||
                (p == Front_Church or East_Church or West_Church)) {
               switch(d) {
                  Front_Church:
                     if (p == West_Church) x =  1;
                     if (p == East_Church) x = -1;
                  West_Church:
                     if (p == North_Church) x =  1;
                     if (p == Front_Church) x = -1;
                  North_Church:
                     if (p == East_Church) x =  1;
                     if (p == West_Church) x = -1;
                  East_Church:
                     if (p == Front_Church) x = 1;
                     if (p == North_Church) x = -1;
                  default:
                     x = 0;}
               self.chain_length = self.chain_length + x;
               if ((self.chain_length > -4) && (self.chain_length < 4)) {
                  move self to p;
                  if (self.is_mad)
                     "^Hunden störtar efter dig i blint raseri!";
                  "^Med ett vagt morrande följer hunden efter dig mot ", (the) p,
                  ", med kedjan släpande efter sig.";}
               else {
                  if (self hasnt general) {
                     give self general;
                     self.is_following = false;
                     self.chain_length = self.chain_length - x;

                     "^Hunden slungar sig mot dig, men stannar plötsligt
                     till med ett ryck och ett bestört gläfsande. Den har
                     löpt ut med kedjan, en kedja som nu är lindad hela vägen
                     runt kyrkan. Hunden gläfser vildsint men maktlöst efter dig
                     medan du fortsätter runt hörnet.";}}}
            else {
               self.is_following = false;
               move self to Front_Church;
               self.chain_length = 0;
               "^Hunden stannar motvilligt, och lommar sen tillbaks mot kyrkans framsida.";}}}
      rtrue;],
   has male animate uter;

Fake_Object Chain "kedja"
   with name
      'tung' 'tunga' 'kedja' 'kedjan' 'stål' 'stålkedja' 'stålkedjan' 'koppel' 'kopplet',
   short_name_indef "kedja",
   short_name_def "kedjan",
   description
      "En tung kedja av stål. Den är en smula rostig men är fortfarande
    mer än tillräcklig för att hålla hunden kopplad.",
   describe [;
      if (Dog has general) {
         switch (location) {
            Front_Church:
               if (Dog notin location) {
                  print "^Den tunga kedjan löper från en av ekarna runt hörnan åt nord";
                  if (Dog.chain_length > 0) "väst."; "öst.";}
            East_Church:
               if (Dog notin location)
                  "^En tung kedja löper runt kyrkan åt nordväst och sydväst.";
            West_Church:
               if (Dog notin location)
                  "^En tung kedja är dragen runt kyrkan, och försvinner åt
          nordöst och sydöst.";
            North_Church:
               "^En tung kedja löper mellan östra och västra hörnet.";}}
      rtrue;],
      before [;
         Take, Pull:
            "Hunden skulle förmodligen inte uppskatta om du drog i kedjan.";],
      found_in Front_Church East_Church West_Church North_Church,
      has static uter;

Outside Top_Of_Tree "Uppe i eken"
   with description
      "Stormen får trädgrenarna att kränga våldsamt, men nära stammen är det
      lite lugnare. Trädet ger en förträfflig utsikt över kyrkan och
      nejden. Själva kyrkan tronar ovanpå en kulle omgiven av skog.
      Vid foten av kullen skymtar ett litet samhälle. Dess ljus tindrar
      svagt genom stormen.
      ^^En av trädgrenarna sträcker sig norrut in mot kyrktornet. En
      fönsterglugg står öppen just där trädgrenen slutar.",
   before [;
      Jump:
         deadflag = 1;
         "Du kastar all försiktighet överbord och dig själv ut ur trädets krona,
          ut i tomma intet. Vinden ylar i dina öron medan du sveper ner mot
          marken. Det sista du ser är hur blixten ristar kyrktornets silhuett
          mot himlen.";
      Climb:
         if (noun == Railing_Top) rfalse;
         if (noun == Tree_Branch) rfalse;
         if (Railing_Top.is_over ~= Front_Church)
            "Du kan inte klättra längre upp, och trädstammen är alltför hal för att klättra nedför.";
         else
            <<Climb Railing_Top>>;
      Drop:
         move noun to Front_Church;
         print_ret (The) noun, " singlar ner genom grenarna mot marken.";],
   n_to Belfry,
   in_to Belfry,
   d_to [;if (Railing_Top.is_over ~= Front_Church)
             "Du är så högt ovanför marken att du inte våga hoppa ner,
             och trädstammen är för hal för att klättra.";
          else
             return Front_Church;];

Object Tree_Branch "trädgren"
   with name
      'ek' 'träd' 'trädgren' 'trädgrenen' 'gren' 'grenen',
   short_name_indef "trädgren",
   short_name_def "trädgrenen",
   description
      "Grenen svajar oroväckande men verkar hålla din tyngd. Den sträcker sig nästan
      hela vägen fram till fönstergluggen i kyrktornet.",
   before [;
      Take:
         "Grenen sitter fast i trädet.";
      Climb:
         PlayerTo(Belfry,2);],
   found_in Top_Of_Tree Belfry,
   has scenery uter;

Object Sidewalk "trottoar" Front_Church
   with name
      'trottoar' 'trottoaren' 'väg' 'vägen' 'stenig' 'stentäckt' 'stenar' 'sten' 'stenplatta' 'stenplattor' 'platta' 'plattor',
   short_name_indef "trottoar",
   short_name_def "trottoaren",
   description
      "En bred stentäckt väg leder mellan de två ekarna till kyrkans framsida.
      Jorden har satt sig, och stenplattorna lutar på märkliga vis. Gräs och ogräs
      växer i fogarna.",
   before [;
      Take:
         "Du försöker lyfta en av stenplattorna, men den slinker ur ditt grepp
         faller tillbaks med ett sugande plask.";],
   has scenery uter;

Object Driveway "infart"
   with name
      'infart' 'infarten' 'grus' 'grustäckt' 'grusbetäckt' 'grusig' 'grusiga' 'väg' 'vägen',
   short_name_indef "infart",
   short_name_def "infarten",
   description
      "En gropig, grustäckt väg.",
   found_in East_Church Front_Church,
   has scenery uter;

Object Ivy "murgröna"
   with name
      'murgröna' 'murgrönan' 'klätterväxt' 'klätterväxter' 'löv' 'löven' 'rankor' 'rankorna',
   short_name_indef "murgröna",
   short_name_def "murgrönan",
   description [;
      if (location == Graveyard)
         "Murgrönan växer tjock och frodig här på ett närmast olustigt vis. Lövens
         former ter sig inte helt naturliga, och du undrar vad för slags egenartad
         näring den suger till sig ur den multnande jorden.";
      else
         "Murgrönan tycks sjuk och gulnande, men klamrar sig likväl fast vid kyrkans
         vägg som i ett försök att fjärma sig från den steniga marken.";],
   before [;
      Climb:
         "Regnmättade rankor spricker loss i dina händer och täcker dig med
         fuktiga lövs och klistrig sav.";
      Take:
         "Rankorna lossnar utan problem, men de bibehåller ett envetet grepp om
         marken som du inte förmår bryta.";
      Burn:
         "Rankorna är allför fuktiga för att fatta eld.";],
   found_in Graveyard North_Church,
   has scenery uter;

! Representerar alla träd i spelet.
Object Forest "ekar"
   with name
      'ek' 'ekar' 'eken' 'ekarna' 'träd' 'träden' 'trädet' 'ekträd' 'ekträdet' 'ekträden' 'skog' 'skogen' 'grenar' 'knotig' 'knotiga' 'trädstam' 'trädstammar' 'trädstammarna',
   parse_name [ wd adj_count noun_count;
        if (parser_action == ##TheSame) return -2;
        wd = NextWord();
        while (wd ~= 0) {
          if (Adjective_Old(wd)) {
            adj_count++;
          } else if (WordInProperty(wd, self, name)) {
            noun_count++;
          } else {
            return 0;
          }
          wd = NextWord();
        }
        if (noun_count > 0) return noun_count + adj_count;
        return 0;
        ],
   short_name_indef "ekar",
   short_name_def "ekarna",
   description [;
      if (location == Graveyard)
         "Dessa uråldriga, knotiga gamla träd omringar kyrkogården och har
         börjat inta den. Stormen sliter vildsint i dem, allt medan deras grenar
         sträcker sig mot himlen och rötterna suger en ohelig näring ur sprickorna
         av gammal stenläggning.";
      else if (location == Front_Church)
         "Dessa massiva träd måste vara hundratals år gamla. De tornar upp sig över kyrkans
         framsida, och sträcker sig nästan upp till kyrktornet. Till skillnad från skogen
         som omger resten av området tycks de nästan onaturligt frodiga, med rena och raka
         grenar och mörkt, lummigt lövverk.";
      else
         "Träden vajar kraftigt i vinden, och deras förvridna grenar tycks sträcka
         sig ut för att klösa kyrkans fasad. I deras grenverk bor varken fåglar
         eller skogsdjur, då en motbjudande känsla av fiendskap mot varje
         sådan inkräktare tycks ruva över dem.";],
   before [;
      Climb:
         if (location == Front_Church && Railing_Section.is_under == Top_Of_Tree)
            <<Go Railing_Section>>;
         else
            "Trädstammen är för slipprig och regnvåt för att klättra.";],
   found_in Graveyard North_Church East_Church Top_Of_Tree Belfry Front_Church,
   has scenery pluralname;

Fake_Object Outside_Storm "storm"
   with name
      'storm' 'stormen' 'vind' 'vinden' 'åska' 'åskan' 'sky' 'skyn' 'blixt' 'blixten' 'blixtar' 'blixtarna' 'åskväder' 'åskvädret' 'blåst' 'blåsten' 'oväder' 'ovädret',
   short_name_indef "storm",
   short_name_def "stormen",
   description
      "Den ylande vinden släpar molnen längs himlen. Blixten
    flackar nästan utan uppehåll, den belyser landskapet i svartvita
    konturer, och ljudet av åskans hammarslag blandas med stormens
    tjut till en vansinnig kakafoni.",
   before [;
      Take, Push, Pull, Touch, Knock:
         "Dina händer erbjuder föga skydd mot stormen.";],
   found_in [; if ((location ofclass Outside) || (location == Grave) ||
                   (location == Belfry)) rtrue;],
   has scenery uter;

Object Rain "regn"
   with name
      'regn' 'regnet' 'skyfall' 'skyfallet',
   short_name_indef "regn",
   short_name_def "regnet",
   description
      "Regnet öser ner så hårt att det gör ont, och kraften i dess
      anslag är nära att förblinda dig.",
   before [;
      Take, Push, Pull, Touch, Knock:
         "Du är redan dyblöt.";],
   found_in [; if ((location ofclass Outside) || (location == Grave)) rtrue;],
   has scenery neuter;

Object Muddy_Ground "lera"
   with name
      'lera' 'leran' 'lerig' 'leriga' 'gyttja' 'gyttjan' 'gyttjig' 'gyttjiga' 'dy' 'dyn' 'dyig' 'dyiga' 'fuktig' 'fuktiga' 'jord' 'jorden' 'mark' 'marken',
   short_name_indef "lera",
   short_name_def "leran",
   description
      "Ett tunt lager lera täcker den steniga jorden.",
   before [;
      Take, Push, Pull, Touch, Knock:
         "Du tar en handfull av lerig jord, men regnet sköljer bort den.";
      Dig:
         "Du kan bara gräva några tum innan du finner sten, och regnet sköljer
     omgående in mer lera så att hålet du gjorde fylls igen.";],
   found_in [; if (location ofclass Outside) rtrue;],
   has scenery uter;

Object Grass_Weed "gräs och ogräs"
   with name
      'gräs' 'och' 'ogräs' 'gräset' 'ogräset',
   short_name_indef "gräs och ogräs",
   short_name_def "gräset och ogräset",
   description
      "Kyrkan omges av allsköns gräsväxter och ogräs. De ger ett sjukligt
    intryck. Vissa reser sig i klungor som når till ditt knä, andra har
    vinden pressat platt mot marken.",
   before [;
      Take, Push, Pull, Touch, Knock:
         "Du drar upp en näve och skärskådar dem ett ögonblick, innan du
     låter dem falla. Du ser ingen anledning att bära dem med dig.";
      Burn:
         "Gräset och ogräset är för fuktigt för att kunna fatta eld.";],
   found_in [; if ((location ofclass Outside) && (location ~= Graveyard))
      rtrue;],
   has scenery neuter;

Fake_Object Church "kyrka"
   with name
      'kyrka' 'kyrkan' 'kyrkans' 'byggnad' 'byggnaden' 'byggnadens' 'tak' 'taket' 'hus' 'huset',
   short_name_indef "kyrka",
   short_name_def "kyrkan",
   parse_name [ wd adj_count noun_count;
        if (parser_action == ##TheSame) return -2;
        wd = NextWord();
        while (wd ~= 0) {
          if (Adjective_Old(wd)) {
            adj_count++;
          } else if (WordInProperty(wd, self, name)) {
            noun_count++;
          } else {
            return 0;
          }
          wd = NextWord();
        }
        if (noun_count > 0) return noun_count + adj_count;
        return 0;
        ],
   description
      "Den gamla kyrkan har sett bättre tider. Fasaden var en gång målad, men nu
      har färgen flagnat och släppt från ruttnande brädor. De flesta av fönstren
      har spikats igen, och ur de som finns kvar har glaset för länge sedan fallit.
      Tegelpannorna har lossnat från ett tak som sviktar oroväckande. Från byggnadens
      ena hörn reser sig ett kyrktorn med sprucket krön, och över hela huset
      vilar en vag olustkänsla av förfall.",
   describe [;
      switch (location) {
         Graveyard:
            "^En kyrka tornar upp sig i öst. Dess igenspikade fönster inger känslan
            av att den håller för ögonen för att skymma sikten mot kyrkogården.";
         West_Church:
            "^I öster erbjuder kyrkoväggen en smula lä från vinden.";
         North_Church:
            "^Kyrkans södra vägg ruvar tomt framför dig.";
         East_Church:
            "^Åt väster stirrar kyrkan ut över gläntan.";
         Front_Church:
            "^Kyrkan och dess torn tornar upp sig mot norr. De öppna fönstergluggarna
            tycks blänga klandrande ner mot alla som kommer nära.";
         Top_Of_Tree:
            "^Kyrktornet tornar upp sig åt norr.";}],
   found_in [; if ((location ofclass Outside) && (location ~= Porch)) rtrue;],
   has static uter;

Object Steeple "kyrktorn"
   with name
      'kyrktorn' 'kyrktornet' 'torn' 'tornet' 'klockstapel' 'klocktorn',
   short_name_indef "kyrktorn",
   short_name_def "kyrktornet",
   description
      "Kyrktornet sträcker sig från byggnadens framsida likt ett knotigt, anklagande
      finger. Tornet slutar i en hackig, trubbig spets. De dekorationer som en
      gång måste ha krönt det är sedan länge borta.",
   found_in [;if (location ofclass Outside) rtrue;],
   has scenery neuter;

Object Windows "fönster"
   with name
      'bly' 'blyinfattad' 'blyinfattade' 'glas' 'fönster' 'fönstren' 'sprucken' 'spruckna' 'igenspikad' 'förseglad' 'kyrkfönster' 'målad' 'målade' 'spikad' 'spikade' 'igenspikad',
   short_name_indef "fönster",
   short_name_def "fönstren",
   description "En gång i tiden lär fönstren ha prytts med berättelser om hopp och syndernas
        förlåtelse i målat, blyinfattat glas. Nu är de igenspikade och förseglade, som om
        kyrkan blundar inför världen utanför.",
   before [;
      Take, Push, Pull, Open, Close, Knock:
         "Brädorna är ordentligt fastspikade.";],
   found_in [;if (location ofclass Outside) rtrue; return (location == Vestibule or Sanctuary or Choir);],
   has scenery pluralname;

!*******************************************************************************
!*** The Porch
!*******************************************************************************
Outside Porch "Kyrkans veranda"
   with description
      "En bred, täckt veranda som löper längs hela kyrkans framsida. Ett vitmålat
      plank omgärdar verandan på tre sidor, förutom söderut där en låg trappa
      går ner mot trottoaren. Två stora fönster är igenspikade, men porten står
      lätt på glänt.",
   n_to Vestibule,
   in_to Vestibule,
   s_to Front_Church,
   out_to Front_Church,
   d_to "Golvet här är gammalt, men fortfarande stadigt.",
   u_to "Staketet är för rangligt för att klättra på.",
   cant_go "Man kan inte lämna verandan åt det hållet.";

[ Railing_Desc;
   print "Planket består av två parallelt löpande träribbor på en
      armslängds avstånd, med serie trästolpar fästa vinkelrätt.";
];

[ Broken_Railing_Desc;
   Railing_Desc();
   " Denna del är avbruten. Om man ställde den på ända skulle
   det här stycket plank vara ungefär dubbelt så långt som du själv.";
];


Object Railing Porch
   with name
      'vit' 'vitt' 'vita' 'plank' 'planket' 'vitmålat' 'vitmålade' 'målat' 'målade',
   short_name_indef "vitt plank",
   short_name_def "vita planket",
   parse_name [ wd noun_count;
        if (parser_action == ##TheSame) return -2;
        wd = NextWord();
        while (wd ~= 0) {
          if (WordInProperty(wd, self, name) || Noun_Fence (wd)) {
            noun_count++;
          } else {
            return 0;
          }
          wd = NextWord();
        }
        return noun_count;
        ],
  description [;
      Railing_Desc();
      print " En del av planket ";
      if (self hasnt general)
         "tycks ganska ranglig.";
      else
         "saknas.";],
   before [;
      Take, Push, Pull, Attack:
         if (self hasnt general) {
            give self general;
            if (action == ##Take)
               move Railing_Section to player;
            else
               move Railing_Section to location;
            "Du bräcker loss ett löst parti av planket.";}
         else
            "Resten av planket verkar hyfsat stabilt.";],
   has static scenery neuter def_article;

Object Railing_Section Limbo
   with
      is_under 0,
   short_name_indef "avbrutet stängsel",
   short_name_def "avbrutna stängslet",
   name
      'lös' 'lösa' 'parti' 'partiet' 'plank' 'planket' 'plankstycke' 'bit' 'biten' 'del' 'delen' 'stycke' 'stycket' 'vit' 'vita' 'stege' 'stegen',
   parse_name [ wd adj_count noun_count;
        if (parser_action == ##TheSame) return -2;
        wd = NextWord();
        while (wd ~= 0) {
          if (Adjective_Broken_Off(wd)) {
            adj_count++;
          } else if (WordInProperty(wd, self, name) || Noun_Fence (wd)) {
            noun_count++;
          } else {
            return noun_count;
          }
          wd = NextWord();
        }
        if (noun_count > 0) return noun_count + adj_count;
        return 0;
        ],
   description [;
      Broken_Railing_Desc();
      rtrue;],
   describe [;
      print "^Det avbrutna partiet stängsel ";
      switch (self.is_under) {
         Steeple_Chamber:
            "står lutat mot väggen under takluckan.";
         Top_Of_Tree:
            "står lutat mot den stora eken.";
         Near_Painting:
            "står lutat mot väggen nära målningen.";
         default:
            "ligger här.";}],
   before [;
      PutUnder:
         if (second == self)
            "Det går inte att luta stängslet mot sig självt.";
         if (second == Lectern)
             <<attack Lectern>>;
         if ((second == Trap_Door || second == Forest || (second == Painting && location == Sanctuary)) == false){
            switch (GetCategory(second)) {
                CAT_DISTANT: "", (The) second, " är inte nära nog.";
                CAT_DETRITUS: "Du hade kunnat ställa stängslet rakt över ", (the) second, ", men vad hade det tjänat till?";
                CAT_IMMATERIAL: "Du kan bara luta stängslet mot ett solitt föremål.";
                CAT_AMORPHOUS: "", (The) second, " klarar inte av att hålla vikten.";
                CAT_SOLID: "Du lyckas inte hitta något stabilt sätt att luta stängslet mot ", (the) second, ".";
                default: "Det går inte att luta stängslet mot ", (the) second, ".";
            }
         }
         if (self.is_under ~= 0)
            "Du justerar stegens placering en aning.";
         move self to location;
         print "Du ställer ";
         if ((location == Vestibule) && (second == Trap_Door)) {
            give self door;
            self.is_under = Steeple_Chamber;
            move Railing_Top to Steeple_Chamber;
            Railing_Top.is_over = Vestibule;
            "försiktigt planket under luckan i taket.";}
         if ((location == Front_Church) && (second == Forest)) {
            give self door;
            self.is_under = Top_Of_Tree;
            move Railing_Top to Top_Of_Tree;
            Railing_Top.is_over = Front_Church;
            "ned planket och lutar det mot sidan av eken.";}
         if ((location == Sanctuary) && (second == Painting)) {
            give self door;
            self.is_under = Near_Painting;
            move Railing_Top to Near_Painting;
            Railing_Top.is_over = Sanctuary;
            "planket och lutar det mot väggen invid målningen.";}
         "", (the) self, " mot ", (the) second, ".";
      Take:
         give self ~door;
         self.is_under = 0;
         move Railing_Top to Limbo;
         Railing_Top.is_over = 0;
      Climb:
         if (self.is_under ~= 0) {
            print "Med stolparna som stegpinnar klättrar du försiktigt upp
            längs planket.^";
            <<Go self>>;}
         else
            "Stegen leder ingenvart.";],
   door_dir u_to,
   door_to [;
      if (self.is_under ~= 0) {
         if (self.is_under == Steeple_Chamber) Achieved(1);
         return self.is_under;}
      "Stegen leder ingenvart.";],
   has open neuter def_article;

Object Railing_Top "ovanpå planket" Limbo
   with
      is_over 0,
   short_name_indef "ovanpå planket",
   short_name_def "ovanpå planket",
   parse_name [ wd adj_count noun_count;
        if (parser_action == ##TheSame) return -2;
        wd = NextWord();
        while (wd ~= 0) {
          if (Adjective_Broken_Off(wd)) {
            adj_count++;
          } else if (WordInProperty(wd, self, name) || Noun_Fence (wd)) {
            noun_count++;
          } else {
            return 0;
          }
          wd = NextWord();
        }
        if (noun_count > 0) return noun_count + adj_count;
        return 0;
        ],
      name
      'ovan' 'ovanpå' 'lös' 'lösa' 'plank' 'planket' 'plankstycke' 'plankstycket' 'bit' 'biten' 'del' 'delen' 'stycke' 'stycket' 'vit' 'vita',
   description [;
      Broken_Railing_Desc();
      rtrue; ],
   describe [;
      switch (self.is_over) {
         Vestibule:
            "^Ena änden på det abrutna stängslet sticker upp genom luckan i golvet.";
         Front_Church:
            "^Under dig står det avbrutna partiet stängsel lutat mot trädet som en stege.";
         Sanctuary:
            "^Du står och balanserar längst upp på det avbrutna stycket stängsel.";
         default:
            "Du står på en bugg!";}],
   before [;
      Take:
         "Du försöker dra upp stängslet efter dig, men du får inte grepp.";
      Climb:
         <<Go self>>;],
   door_dir d_to,
   door_to [; return self.is_over;],
   has open door neuter;

!*******************************************************************************
!*** The inside
!*******************************************************************************
Class Inside class Area
   with
      u_to "Du ser bara ett innertak där uppe.",
   each_turn [;
      if (random(5) == 1) {
         switch (random(5)) {
            1: "^En ensam åskknall får byggnaden att skaka.";
            2: "^Du hör en öronbedövande skräll och ser en ljusblixt genom sprickorna
               i de igenspikade fönstren.";
            3: "^Byggnaden darrar till av en plötslig vindby.";
            4: "^Ljuset av en blixt skymtar mellan glappet i brädorna, och åskans
            dån följer omedelbart därpå.";
            5: "^Under ett kort ögonblick smattrar haglet på takpannorna.";}}];

Inside Vestibule "Förrummet"
   with description
      "Detta trånga utrymme är kyrkans förrum, och uppenbart
      eftersatt efter åratal av misskötsel. Trägolvet är murket och
      plankorna har slagit sig, och vad som en gång lär ha varit
      målade glasfönster är nu sönderslagna och igenspikade.
      Smuts och skräp täcker golvet. Ett par dubbeldörrar vetter
      söderut ut ur kyrkan, och en välvd passage leder norrut in i
      själva kyrkan. I taket ovanför gapar en öppen lucka,
      ungefär dubbelt så högt upp som du är lång.",
   n_to Sanctuary,
   in_to Sanctuary,
   s_to Porch,
   out_to Porch,
   u_to [;if (Railing_Section.is_under ~= Steeple_Chamber)
             "Takluckan är alldeles för högt upp för att du ska kunna nå dit.";
          else
             return Railing_Section;];

Object Trap_Door Vestibule
   with name
    'lucka' 'luckan' 'hål' 'hålet' 'öppning' 'öppningen',
   parse_name [ wd noun_count;
        if (parser_action == ##TheSame) return -2;
        wd = NextWord();
        while (wd ~= 0) {
          if (WordInProperty(wd, self, name)) { wd = NextWord(); noun_count++; }
          else {
            if (location == Steeple_Chamber) while (wd == 'golvlucka' or 'golvluckan' or 'golvdörr' or 'golvdörren') { wd = NextWord(); noun_count++; }
            else while (wd == 'taklucka' or 'takluckan' or 'taköppning' or 'taköppningen') { wd = NextWord(); noun_count++; }
            if (wd == 'i//' or 'inom') {
              wd = NextWord();
              if ((location == Steeple_Chamber && (wd == 'marken' or 'golvet')) || (location == Vestibule && wd == 'taket') )
              {
                wd = NextWord();
                noun_count = noun_count +2;}
              else return noun_count;
            }
            return noun_count;
          }
        }
        ! This may be redundant.
        while (WordInProperty(wd, self, name)) { wd = NextWord(); noun_count++; }
        return noun_count;
        ],
   short_name_indef [; if (location == Vestibule) print "taklucka"; else print "golvlucka"; rtrue; ],
   short_name_def [; if (location == Vestibule) "takluckan"; else print "golvluckan"; rtrue; ],
   description [;
   print "Öppningen i ";
   if (location == Vestibule) {
      print "taket tycks leda till ett annat rum ovanför";
      if (Railing_Section.is_under ~= Steeple_Chamber) {
        print " men det tycks inte finnas något sätt att nå upp dit";
      }
   } else {
      print "golvet leder ner till kyrkans förrum. Det är långt
      ner till golvet";
      if (Railing_Section.is_under == Steeple_Chamber)
        print ", men staketet står lutat nedanför som en stege";
   }
   ".";],
   before [;
      Open:
          if (location == Vestibule && Railing_section.is_under ~= Steeple_Chamber) { "Luckan är redan öppen, och går i vilket fall inte att nå härifrån."; }
          else { "Luckan är redan öppen."; }
      Close:
          if (location == Vestibule && Railing_section.is_under ~= Steeple_Chamber) { "Du kan inte nå luckan härifrån."; }
          else { "Luckan är fixerad och kan inte stängas."; }],
   found_in Vestibule Steeple_Chamber,
   has scenery uter;

Inside Sanctuary "Långhuset"
   with description
      "Det som en gång var kyrkans hjärta står nu kallt, mörkt
      och tomt. De sviktande kyrkbänkarna är belamrade med bråte
      och av de målade glasfönstren finns bara skärvor kvar. Det
      mesta av dekoren har försvunnit, men ett stort porträtt av
      Kristus sirrar ner på dig ovanför valvgången åt söder.
      Predikstolen står fortfarande kvar norrut.",
   before [;
      Pray:
         "Du ber, men förgäves. På något vis kan du känna att dina böner
         hörs, men att den som hör inte lyssnar.";],
   n_to Choir,
   in_to Choir,
   s_to Vestibule,
   out_to Vestibule,
   u_to [;if (Railing_Section.is_under == Near_Painting)
             return Railing_Section;
          "Du ser bara ett innertak där uppe.";];

Fake_Object Distant_Choir "predikstol"
   with name
      'predikstol' 'predikstolen' 'plattform' 'plattformen' 'trä' 'podium' 'podie' 'podiet',
   short_name_indef "predikstol",
   short_name_def "predikstolen",
   description
      "På avstånd kan du se en enkel talarstol i trä, placerad på en
    plattform i mitten av koret.",
   found_in Sanctuary Near_Painting,
   has static scenery uter;

Object Pews "kyrkbänkar" Sanctuary
   with name
      'bänk' 'bänken' 'bänkar' 'bänkarna' 'kyrkbänk' 'kyrkbänken' 'kyrkbänkar' 'kyrkbänkarna' 'sittplats' 'sviktande',
   short_name_indef "kyrkbänkar",
   short_name_def "kyrkbänkarna",
   description
      "I forna tider hade församlingen tagit upp varenda sittplats.
      Nu ligger damm och bråte överallt.",
   after [;
      Enter:
         "Du sätter dig ned på en av bänkarna. För ett andetag tycks stormen
         dra bort, och du känner ett ögonblick av frid.";],
   has static scenery enterable open transparent pluralname;

Inside Near_Painting "Nära tavlan"
   with description
      "Du står ovanpå stycket av plank nära den stora målningen av Jesus.
      Härifrån kan du skönja att målningens ögon har klippts bort och att
      det finns hål i väggen bakom dem.",
   before [;
      Drop:
         move noun to Sanctuary;
         print_ret (The) noun, " singlar ner mot golvet under dig.";],
   d_to Railing_Top,
   cant_go "~Ner~ är nog det enda valet här.";

Object Eyes "målningens ögon" Near_Painting
   with name
      'ögon' 'ögonen' 'blick' 'blicken',
   parse_name [ wd adj_count noun_count;
        if (parser_action == ##TheSame) return -2;
        wd = NextWord();
        while (wd == 'målningens' or 'tavlans' or 'bildens' or 'jesus')
            { wd = NextWord(); adj_count++; }
        while (WordInProperty(wd, self, name))
            { wd = NextWord(); noun_count++; }
        if (noun_count > 0) return noun_count + adj_count;
        return 0;
        ],
   short_name_indef "ögon",
   short_name_def "ögonen",
   description
      "Någon har klippt bort ögonen på målningen. Bakom dem finns det
      hål och ett öppet utrymme.",
   before [;
      Search:
         if (Curtain has general)
            "Du blickar in i det lilla rummet i kyrktornet. Du kan se bortom ett
            bleknat lila skynke till ett litet bord på vilket det vilar
            två tunga glasflaskor.";
         else
           "Du blickar in i ett litet trångt utrymme täckt av ett bleknat lila skynke.";],
    has pluralname scenery;

Fake_Object Distant_Bottles Near_Painting
   with name
      'tung' 'tunga' 'tjock' 'tjocka' 'flaska' 'flaskor' 'glasflaska' 'glasflaskor' 'flaskorna' 'glasflaskorna',
   short_name_indef "flaskor",
   short_name_def "flaskorna",
   description
      "Två flaskor i tjockt glas vilar på bordet. En skimrar med ett klart grönt sken,
       den andra med ett blekt blått ljus.",
   has scenery pluralname;

Bottle Distant_Green_Bottle "grön flaska" Near_Painting
   with
      colour "klart grönt",
   name
      'skarp' 'skarpt' 'intensiv' 'intensivt' 'grön' 'gröna' 'flaska' 'flaskan',
   short_name_indef "grön flaska",
   short_name_def "gröna flaskan",
   before [;
      Take, Attack, Open, ThrownAt:
         "Du kan inte göra något med den gröna flaskan härifrån.";],
   has scenery uter def_article;

Bottle Distant_Blue_Bottle "blå flaska" Near_Painting
   with
      colour "blekt blått",
   name
      'blek' 'blekt' 'mjuk' 'mjukt' 'svag' 'svagt' 'blå' 'blåa' 'flaska' 'flaskan',
   short_name_indef "blå flaska",
   short_name_def "blåa flaskan",
   before [;
      Take, Attack, Open, ThrownAt, Knock:
         "Du kan inte göra något med den blåa flaskan härifrån.";],
   has scenery uter def_article;

Inside Choir "Koret"
   with
   description
      "Från den här upphöjda plattformen kan du se hela salen. Podiet är tomt
      förutom talarstolen som står ovanpå det. En smal dörr skymtar bakom
      podiet åt norr.",
   before [;
      Pray:
         "Du ber, men förgäves. Något säger dig att dina böner hörs, men att den
         som hör inte lyssnar.";],
   s_to Sanctuary,
   out_to Sanctuary,
   n_to Office,
   in_to Office;

 Object Lectern Choir
   with name
      'ställ' 'stället' 'talarstol' 'talastolen' 'snidat' 'snidad' 'snidade' 'trä',
   short_name_indef "talarstol",
   short_name_def "talarstolen",
   description [;
      print "Podiet är snidat i mörkt trä och stort nog för att hålla en
      bibel. Här torde en gång kyrkoherden ha stått inför sin församling, men
      allt du kan se från där du står är rader av tomma kyrkbänkar och en
      bild på Jesus som stirrar tomt ner från den södra väggen.";
      if (parent(Book) == self)
         " En tung svart bok ligger uppslagen på podiet.";
      else
         new_line; rtrue;],
   before [;
      Enter: "Du saknar publik.";
      Push, Pull, Turn, Climb, Attack: "Talarstolen är för bräcklig för sådant.";
   ],
   has supporter static scenery neuter;

Object Podium Choir
   with name
      'podium' 'podiet' 'plattform' 'plattformen',
   short_name_indef "plattform",
   short_name_def "plattformen",
   description
      "En plattform i trä täcker korets golv. Dess enda utmärkande detalj är
      predikstolen i dess mitt.",
   before [;
      Enter: "Du står redan på ovanpå det.";
      Exit: "Förutom att gå norrut eller söderut finns ingen väg av plattformen.";
      Receive: <<drop noun>>;
   ],
   has supporter static scenery neuter;

Object Book Podium
   with name
      'tung' 'tunga' 'uppslagen' 'uppslagna' 'svart' 'svarta' 'bok' 'boken',
   short_name_indef "svart bok",
   short_name_def "svarta boken",
   description
      "Först trodde du att boken var en bibel, men nu inser du att du misstagit
      dig. Mellan dess svarta läderbundna pärmar finns långa stycken som ger dig
      rysningar, och en dunkel erinran om något du läst förut.",
   before [;
      Open:
         "Boken är redan uppslagen.";
      Read:
         "Större delen av boken är anfrätt av fukt och råttor, men
          några få passager är forfarande läsliga. Du fastnar
          särskilt för en av dem.^^~Vägen till att förstå någon är
          genom ögonen, då ögonen är själens fönster. Genom ögonen
          kan känsliga sinnen förnimma själen, och genom ögonen kan
          själen också förledas av en som lovat sig till de som väntar.~";
      Take:
         give self ~concealed;],
   has concealed uter def_article;

Object Painting
   with name
      'tavla' 'tavlan' 'målning' 'målningen' 'porträttet' 'porträtt' 'av' 'Jesu' 'Jesus' 'Krist' 'Kristus' 'Frälsare' 'Frälsaren' 'Herre' 'Herren',
   short_name_indef "målning",
   short_name_def "målningen",
   description
      "Medan du betraktar målningen upplever du hur ett minne stiger upp till ytan.
       Du minns att du stod i just den här kyrkan, såg mot just den här målningen.
       Och du minns hur den stirrade tillbaks på dig, med illvilliga, klargröna ögon...",
   before [;
      Take, Push, Pull, Attack, Knock, Touch:
         "Målningen hänger för högt upp på väggen för att du ska kunna göra något med den.";],
   found_in Sanctuary Choir Near_Painting,
   has scenery uter;

Object Distant_Eyes
   with name
      'ögon' 'ögonen' 'blick' 'blicken',
   parse_name [ wd adj_count noun_count;
        if (parser_action == ##TheSame) return -2;
        wd = NextWord();
        while (wd == 'målningens' or 'tavlans' or 'bildens' or 'jesus')
            { wd = NextWord(); adj_count++; }
        while (WordInProperty(wd, self, name))
            { wd = NextWord(); noun_count++; }
        if (noun_count > 0) return noun_count + adj_count;
        return 0;
        ],
   before [;
      Take, Push, Pull, Attack, Knock, Touch:
         "Målningen hänger för högt upp på väggen för att du ska kunna göra något med den.";],
   short_name_indef "ögon",
   short_name_def "ögonen",
   description
      "Någonting underligt vilar över målningens ögon, men det är för långt bort
      för att du ska kunna se tydligt.",
   found_in Sanctuary Choir,
   has scenery pluralname;

Inside Office "Sakristian"
   with description
      "Detta lilla kontor ger samma nedgångna intryck som kyrkan i stort. Bråte
      är utspritt över golvet och en rännil av vatten sipprar in genom brädorna
      som spikats över ett sönderslaget fönster. I mitten av rummet står ett
      skrivbord, och en tom bokhylla står lutad mot ena väggen. En hög aska
      på golvet ger vid hand var en eld en gång brunnit. Dörren åt söder leder
      tillbaks till långhuset.",
   s_to Choir,
   out_to Choir;

Object Desk "skrivbord" Office
   with name
      'skrivbord' 'skrivbordet' 'bord' 'bordet',
   parse_name [ wd adj_count noun_count;
        if (parser_action == ##TheSame) return -2;
        wd = NextWord();
        while (wd ~= 0) {
          if (Adjective_Old(wd)) {
            adj_count++;
          } else if (WordInProperty(wd, self, name)) {
            noun_count++;
          } else {
            return 0;
          }
          wd = NextWord();
        }
        if (noun_count > 0) return noun_count + adj_count;
        return 0;
        ],
   short_name_indef "skrivbord",
   short_name_def "skrivbordet",
   description
      "Ett gammalt skrivbord i trä. Det är fårad och vattenskadad, och har uppenbarligen
      stått här länge.",
   has supporter static scenery transparent neuter;

Object Bookshelf "bokylla" Office
   with name
      'ranglig' 'rangliga' 'hylla' 'hyllan' 'bokhylla' 'bokhyllan' 'byrå' 'byrån',
   parse_name [ wd adj_count noun_count;
      if (parser_action == ##TheSame) return -2;
      wd = NextWord();
      while (wd ~= 0) {
        if (Adjective_Old(wd)) {
          adj_count++;
        } else if (WordInProperty(wd, self, name)) {
          noun_count++;
        } else {
          return 0;
        }
        wd = NextWord();
      }
      if (noun_count > 0) return noun_count + adj_count;
      return 0;
      ],
   short_name_indef "bokhylla",
   short_name_def "bokhyllan",
         description
      "En ranglig bokhylla i trä.",
   before [;
      Push, Pull:
         if (self hasnt general) {
            move Grimoire to location;
            give self general;
            "Du föser bokhyllan åt sidan, och blottar en gammal bok med
            märkligt rödaktiga pärmar.";}
      Search:
         if (self hasnt general)
            "Någonting tycks sitta fastklämt mellan bokhyllan och väggen.";],
   has container static scenery transparent uter;

Object Grimoire "gammal trolldomsbok"
   with name
      'magisk' 'magiska' 'bok' 'boken' 'trolldomsbok' 'häxbok' 'häxboken' 'formelbok' 'magibok' 'magiboken' 'röd' 'röda' 'rödaktig' 'besvärjelsebok' 'ritual' 'rit' 'riter' 'procedur',
   parse_name [ wd adj_count noun_count;
        if (parser_action == ##TheSame) return -2;
        wd = NextWord();
        while (wd ~= 0) {
          if (Adjective_Old(wd)) {
            adj_count++;
          } else if (WordInProperty(wd, self, name)) {
            noun_count++;
          } else {
            return 0;
          }
          wd = NextWord();
        }
        if (noun_count > 0) return noun_count + adj_count;
        return 0;
        ],
   short_name_indef "gammal trolldomsbok",
   short_name_def "gamla trolldomsboken",
   description
      "Ett mycket gammalt verk, vars pärmar är bundna i ett märklig rödaktigt material.
      Först trodde du det var läder, men det tycks mer påminna om ormskinn. Boken tycks
      vara någon form av underlig trolldomsbok eller besvärjelsebok, och behandlar
      i huvudsak själen eller anden.",
   before [;
      Read:
         "Boken beskriver flertalet ritualer och procedurer med inriktning på
         själen eller anden. Den första beskriver en ritual med vilken man kan
         lösgöra själen från någons kropp och placera den i ett särskilt förberett
         kärl. Själen stannar kvar i kärlet även om ägarens kropp skulle dö.^^
         En påföljande ritual kan brukas för att nyttja den bevarade själen som
         behållare för en annan människas synder. Detta låter den som besitter
         en annan människas själ utföra vilka handlingar de än önskar utan att
         behöva svara för dem i efterlivet.";],
   has uter def_article;

Object Journal Desk
   with name
      'bok' 'boken' 'dagbok' 'dagboken' 'liggare' 'liggaren' 'sida' 'sidor' 'blad' 'bladen',
   short_name_indef "gammal dagbok",
   short_name_def "gamla dagboken",
   parse_name [ wd adj_count noun_count;
        if (parser_action == ##TheSame) return -2;
        wd = NextWord();
        while (wd ~= 0) {
          if (Adjective_Old(wd)) {
            adj_count++;
          } else if (WordInProperty(wd, self, name)) {
            noun_count++;
          } else {
            return 0;
          }
          wd = NextWord();
        }
        if (noun_count > 0) return noun_count + adj_count;
        return 0;
        ],
   initial
      "En gammal bok vilar på skrivbordet.",
   description [;
      if (self hasnt general)
         "En bok med tomma sidor, av den sort man kunnat använda som liggare
         eller dagbok. Oturligt nog har någon slitit ut de flesta
         sidorna, och det enda som återstår är tomma blad.";
      else
         "En bok med tomma sidor, av den sort man kunnat använda som liggare
         eller dagbok. Någon har slitit ut de flesta sidorna, men askan
         har gjort linjerna från det sista inlägget läsliga.";],
   before [;
      Search:
         "När du omsorgsfullt studerar boken märker du att det sista inlägget
         måste ha gjorts med ordentlig kraft. Trycket har gjort märken i bokens
         första tomma sida.";
      Read:
         if (self hasnt general)
            "Det finns inget att läsa på de tomma sidorna.";
         else
            Achieved(3);
            print "Du läser det sista inlägget från dagboken.^^";
            style underline;
            print " ~Jag mäktar inte fortsätta. Ju bättre jag förstår henne
            desto mer står det klart att jag inte kan fortsätta fördärva
            henne. Innan jag tog henne var hon ren och oskuldsfull, och
            trots allt vad jag gjort är det den oskulden som låtit henne
            behålla sin tillit.
            Jag kan bara hoppas att mina synder inte sudlat henne bortom
            all räddning, och att hon när jag ger henne fri kommer att
            färdas till sin tillägnade plats... såsom jag måste gå till
            min egen.";
            style roman;
            "~";],
   has uter def_article;

Object Pile_of_Ash Office
   with name
      'hög' 'högen' 'aska' 'askan' 'eld' 'elden' 'brasa' 'brasan' 'flagor' 'flagorna' 'askflagor' 'sot' 'soten',
   short_name_indef "aska",
   short_name_def "askan",
   description
      "Det tycks som en ordentligt stor eld brunnit här en gång. Det är förvånansvärt
      att inte hela rummet fattade eld. Från spåren av kvarvarande fragment verkar det
      som om vem som nu anlade elden brände upp böcker, förmodligen från den nu tomma
      bokhyllan. Bland askflagorna finner du ett antal spetsiga glasskärvor.",
   before [;
      Take:
         if (Ash notin player) {
            move Ash to player;
            "Du fyller en näve med aska.";}
         else
            "Du har redan näven full av aska.";
      Search:
         <<Examine Broken_Bottle>>;],
   has scenery uter;

Object Ash Limbo
   with name
      'näve' 'näven' 'handfull' 'handfullen' 'av' 'aska' 'askan' 'sot' 'soten',
   description
      "En näve full av gammal aska.",
   short_name_indef "handfull aska",
   short_name_def "handfullen aska",
   before [;
      Drop, ThrowAt:
         move self to Limbo;
         if (location ofclass Outside)
            "Du låter askan falla, och den sprids för vinden.";
         else
            "Du låter askan falla, och den sprids över bråtet på golvet.";
      Rub, Insert, PutOn:
         if (second == Journal) {
            give Journal general;
            "Då du gnider askan mot sidan fylls fördjupningarna in med sot, och
            det sista inlägget blir med ens synligt.";}],
      has uter def_article;

Object Broken_Bottle Office
   with name
      'sprucken' 'spruckna' 'vass' 'vassa' 'skarp' 'skarpa' 'spetsig' 'spetsiga' 'glas' 'glaset' 'skärva' 'skärvan' 'skärvor' 'skärvorna' 'glasbit' 'glasbitar' 'fragment' 'fragmenten' 'glasskärvor',
   short_name_indef "glasskärvor",
   short_name_def "glasskärvorna",
   description [;
      print "Ett antal spruckna glasskärvor ligger sammanblandade med askan.
      Att döma av bitarna måste någon slags tjock glasflaska ha krossats
      mot golvet.";
      if (Stopper hasnt moved)
         print " Ett märkligt föremål ligger bland askan och skärvorna. Det ser
         ut som en grov gummiplugg fäst vid en kedja.";
      new_line; rtrue;],
   before [;
      Take:
         "Du sticker dig i fingret på glaset när du försöker plocka upp det.";],
   has pluralname scenery;

Object Stopper Office
   with name
      'grov' 'grova' 'kraftig' 'kraftiga' 'tjock' 'tjocka' 'tung' 'tunga' 'gummi' 'gummiplugg' 'plugg' 'pluggen' 'kork' 'korken' 'kedja' 'kedjan' 'bly' 'vikt' 'vikten' 'blyvikt' 'blyvikten' 'blylod' 'blyloded' 'lod' 'lodet' 'försegling',
   short_name_indef "tung plugg",
   short_name_def "tunga pluggen",
   description
      "En tung gummiförsegling, av den sort som man kunnat använda för att försluta
      en flaska. Resterna av vax som sitter kvar visar var flaskan förseglats
      med pluggen i. En smal kedja hänger från pluggen och slutar i en märkligt
      ristad blyvikt.",
   before [;
      Take:
         give self ~concealed;],
   has concealed uter def_article;

Object Robe Office
   with name
      'broderad' 'broderi' 'broderade' 'röd' 'röda' 'rock' 'rocken' 'mantel' 'manteln' 'dräkt' 'dräkten',
   short_name_indef "broderad rock",
   short_name_def "broderade rocken",
   initial
      "En konstfärdigt broderad rock hänger vid sidan om dörren.",
   description
      "En konstfärdigt broderad röd dräkt, av den sort som bärs under ceremoniella former.",
   has clothing uter def_article;

Inside Steeple_Chamber "Tornrummet"
   with description
      "Detta trånga utrymme ligger kanske halvvägs uppe i
      kyrktornet. Regn och vind från stormen utanför strilar in
      genom en spräckt fönsterram. Det snedställda golvet badar
      i regnvatten och halvt upplöst spillning från fåglar
      eller fladdermöss. När du lyfter blicken syns en formlig
      labyrint av balkar och stödbjälkar som håller
      uppe tornet. Regnet strömmar in från en öppning högre i
      tornet och sipprar ut genom den öppna luckan i golvet.",
   n_to Steeple_Door,
   u_to [;if (parent(Rope) ~= location)
             "Du ser inget bra sätt att klättra vidare uppåt från där du står.";
          else
             return Rope;],
   d_to [;if (Railing_Top.is_over ~= Vestibule)
             "Avståndet ner till förrummet är för stort för att du ska kunna landa
             oskadd.";
          else
             return Vestibule;];

Inside Inner_Chamber "Inre kammare"
   with description
      "Stanken som omger dig är kompakt och vämjelig. Inte ens den öppna
      dörren och draget från stormen utanför kan förjaga den.
      Högar av skräp och halvätna matrester täcker golvet. Det finns inga
      fönster, bara den spruckna dörröppningen åt söder. En gammal soffa
      står uppknuffad mot ena väggen och ett tungt bord vilar i dess mitt.
      Norrut är en välvd dörröppning täckt av ett skynke.",
   before [;
      Smell:
         "Rummet stinker av urin, svett och ruttnande mat.";],
   s_to Steeple_Door,
   n_to Curtain;

Object Steeple_Door
   with name
      'dörr' 'dörren' 'tung' 'tunga' 'bastant' 'bastanta' 'splittrad' 'splittrade' 'trä' 'trädörr' 'trädörren' 'splittrad' 'dörrkarm' 'dörrkarmen' 'karm' 'karmen' 'handtag' 'handtaget' 'grov' 'grova' 'grovt',
   short_name_indef [;
      if (self hasnt open)
         print "bastant trädörr";
      else
         print "splittrad dörrkarm";
      rtrue;],
   short_name_def [;
      if (self hasnt open)
         print "bastanta trädörren";
      else
         print "splittrade dörrkarmen";
      rtrue;],
   parse_name [ wd adj_count noun_count;
        if (parser_action == ##TheSame) return -2;
        wd = NextWord();
        while (wd ~= 0) {
          if (Adjective_Shattered(wd)) {
            adj_count++;
          } else if (WordInProperty(wd, self, name)) {
            noun_count++;
          } else {
            return 0;
          }
          wd = NextWord();
        }
        if (noun_count > 0) return noun_count + adj_count;
        return 0;
        ],
   description [;
      if (self hasnt open)
         "Denna bastanta trädörr ser ut att vara nyare och stabilare till sin
         konstruktion än övriga delar av kyrkan. Ett grovt, tungt handtag
         är fäst i dörren.";
      else
         "Dörren har slitits loss från dörröppningen. Det enda som återstår
         är de splittrade resterna av dörrkarmen.";],
   when_open [;
      print "En splittrad dörröppning gapar ";
      if (location == Steeple_Chamber)
         "norrut.";
      else
         "söderut.";],
   when_closed
      "Den bastanta trädörren i norr är stängd.",
   door_to [; if (location == Steeple_Chamber) {
                 Achieved(2);
                 return Inner_Chamber;}
              else
                 return Steeple_Chamber;],
   door_dir [; if (location == Steeple_Chamber) return n_to;
               return s_to;],
   found_in Steeple_Chamber Inner_Chamber,
   has door static openable ~open locked uter def_article;

Object Curtain Inner_Chamber
   with name
      'skynke' 'skynket' 'lila' 'blekt' 'bleknat' 'bleknade' 'purpur' 'draperi' 'draperiet' 'valv' 'valvet' 'valvbåge' 'valvbågen' 'gardin' 'gardinen',
   short_name_indef "skynke",
   short_name_def "skynket",
   description [;
      if (self hasnt general)
         "Färgen på skynket är en gammal, bleknad lila nyans. Det ser ut
         som om det en gång i tiden kan ha varit en bordsduk.";
      else
         <<Examine Mirror>>;],
   describe
      "^Ett lila tygskynke hänger för valvet.",
   before [;
         Push: <<Go n_obj>>; ],
   door_to [;
      if (self hasnt general) {
         give self general;
         move Mirror to location;
         Achieved(5);
         "Du föser draperiet åt sidan och stannar upp. På andra sidan
         av draperiet ligger ett rum ungefär som det du står i. En
         människogestalt står i valvbågen, tydligen på väg mot det du står i,
         och det är åsynen av den gestalten som hejdar dig. Den är, eller
         var, mänsklig... en gång i tiden. Nu är den ett förtvinat stycke
         ruttnande kött. Dess kläder är söndertrasade och den är täckt av
         lera och mögelfläckar. Enstaka hårtestar hänger från huvudsvålen
         och benknotor framträder här och var under den utspända huden.
         Det enda av skepnaden som är oförstörd är dess ögon: blekt blå, och
         nu vidöppet stirrande på dig.^^Varelsen höjer sin hand mot
         dig, och du finner dig göra detsamma. Du sträcker ut armen och
         vidrör...-^^^^...en kall, orubblig yta av polerat glas.^^^^
         I det ögonblicket kommer insikten i vad du sett ifatt dig. Du
         snubblar bakåt från spegeln med ett utrop.";}
      else
         "Du sneglar mot spegeln, men inser att det inte finns någon
         mening med att träda in i dess verklighet, för verkligheten
         i spegeln är din egen.";],
   door_dir n_to,
   has door static open uter;

Object Mirror Limbo
   with name
      'spegel' 'spegeln' 'glas' 'glaset' 'glasspegel' 'spegelglas' 'ram' 'ramen' 'polerad' 'polerade' 'välpolerad' 'gångjärn',
   short_name_indef "spegel",
   short_name_def "spegeln",
   description [;
      if (self hasnt open)
         "En välpolerad spegel fäst i en enkel träram. Spegeln återkastar
         rummet du är i och du finner att du drar undan blicken hellre än att
         betrakta vad du ser. Du märker att spegelns ram har gångjärn på ena
         sidan.";
      else
         "En välpolerad spegel fäst i en enkel träram. Spegeln har svängt ut
         från väggen, och blottat två hål i ungefär ögonhöjd.";],
   before [;
      Open, Pull:
         if (self has open)
            "Spegeln kan inte svänga längre ut från väggen.";
         move Inner_Holes to location;
         give self open;
         "Du drar ut spegeln från väggen, och den svänger åtsidan för att
         blotta två hål i väggen bakom.";
      Close, Push:
         if (self hasnt open)
            "Spegeln hänger på väggen.";
         move Inner_Holes to Limbo;
         give self ~open;
         "Du svänger tillbaks spegeln mot väggen igen.";],
   has static scenery uter;

Object Inner_Holes "holes in the wall" Limbo
   with name
      'ögon' 'ögonen' 'blicken' 'hål' 'kikhål',
   parse_name [ wd adj_count noun_count;
        if (parser_action == ##TheSame) return -2;
        wd = NextWord();
        while (wd == 'målningens' or 'tavlans' or 'bildens' or 'jesus')
            { wd = NextWord(); adj_count++; }
        while (WordInProperty(wd, self, name))
            { wd = NextWord(); noun_count++; }
        if (noun_count > 0) return noun_count + adj_count;
        return 0;
        ],
   short_name_indef "hål i väggen",
   short_name_def "hålen i väggen",
   description
      "De två hålen i väggen är stora som din tumme och sitter ungefär i ögonhöjd.",
   before [;
      Search:
         if (Sanctuary has visited)
            "Du kikar genom hålen och inser att du blickar ner mot kyrkans långhus
            med god utsikt över predikstolen. Från vinkeln är det tydligt att du
            ser ut genom ögonen på målningen av Jesus.";
         else
            "Du kikar genom hålen mot långhuset i en kyrka. Rader av dammiga
            kyrkobänkar vetter bort mot ett upphöjt kor med en predikstol.";],
   has pluralname scenery;

Object Table Inner_Chamber
   with name
      'tung' 'tunga' 'tungt' 'bord' 'bordet' 'bordsyta' 'bordsytan' 'träbord' 'träbordet' 'symbol' 'symboler',
   short_name_indef "tungt träbord",
   short_name_def "tunga träbordet",
   description
      "Ett tungt bord gjort av grovhuggna träplankor. Underliga symboler har ristats
      i träet och bordsytan har märkliga fläckar.",
   before [;
      Take, Push, Pull:
         "Bordet väger för mycket för att flytta på.";
      Turn:
         "En plötslig önskan att möblera om?";],
   has static supporter neuter def_article;

Object Lamp "oil lantern" Table
   with name
      'flämtande' 'lykta' 'lyktan' 'lampa' 'lampan' 'fotogenlampa' 'lanterna' 'ljus' 'ljuset' 'mässing' 'mässings' 'oljelampa' 'oljelykta',
   short_name_indef "lampa",
   short_name_def "lampan",
   description
      "En gammal oljelampa. Möjligtvis i mässing, men väldigt ärgad. Skärmens glas
      är nästan ogenomskinlig av sot och avlagringar.",
   has light uter;

Fake_Object Bottles "par glasflaskor" Table
   with name
      'par' 'paret' 'glasflaskor' 'flaskor',
   short_name_indef "par glasflaskor",
   short_name_def "glasflaskorna",
   initial
      "Ett par tjocka glasflaskor står på bordet.",
   description
      "Flaskorna är i tungt blyglas och plomberad med kork och vax. Inuti
      flaskorna hänger ett underligt format lod av bly likt en pendel på
      ett snöre. Flaskorna ger ifrån sig ett svagt ljussken.",
   before [;
      Take:
         <Take Blue_Bottle>;
         <Take Green_Bottle>;
      Attack:
         if (random(2) == 1)
            <<Attack Blue_Bottle>>;
         else
            <<Attack Green_Bottle>>;],
   has static neuter;

Bottle Blue_Bottle "blue bottle" Table
   with name
      'blek' 'blekt' 'mjuk' 'mjukt' 'svag' 'svagt' 'blå' 'blåa' 'flaska' 'flaskan',
   short_name_indef "blå flaska",
   short_name_def "blåa flaskan",
   colour "blekt blå",
   open_bottle [;
      deadflag = 1;
      "Ett moln av svagt lysande blå dimma stiger från flaskan och hänger i
      luften framför dig. Du känner ett ögonblick av frihet, av förlösning.^^
      Då inser du att ", (the) Old_Man, " börjat skrocka för sig själv. Den
      första smärtstöten genomfar dig. Molnet rycker till och börjar sjunka,
      som om det dras ner av ett starkt sug. Smärtan flammar upp och du faller
      först på knä, sen platt till golvet som om du försöker följa dimman,
      som nu sipprar in genom marken. Smärtan blir olidlig. Det sista du ser,
      just när allt svartnar, är ", (the) Old_Man, " som står över dig och
      skrattar...";],
   has concealed uter def_article;

Bottle Green_Bottle "green bottle" Table
   with name
      'skarp' 'skarpt' 'intensiv' 'intensivt' 'grön' 'gröna' 'flaska' 'flaskan',
   short_name_indef "grön flaska",
   short_name_def "gröna flaskan",
   colour "skarpt grön",
   open_bottle [;
      deadflag = 2;
      Achieved(6);
      print (The) Old_Man, " inser med ens vad du företar dig. Tjutande ~";
      style underline; print "neeej!"; style roman; print "~ vräker han sig mot
      dig";
      if (((location == Inner_Chamber) && (parent(Lamp) == Table)) ||
          ((location ofclass Inside) && (parent(Lamp) == location))) {
         give Lamp general;
         print " och välter i samma rörelse lyktan över ända";}
      if ((location ofclass Inside) && (parent(Lamp) == Player)) {
         give Lamp general;
         print " och slår i samma rörelse lyktan ur ditt grepp";}
      print ". När han sjunker ihop vid dina fötter börjar en lysande grön
      dimma stiga från flaskan och vilar i luften framför honom. ~"; style
      underline; print "Nej"; style roman; print ",~ andas han.^^Så börjar
      dimman sakta sjunka neråt medan han viker sig dubbel och skriker ut
      sin plåga. Medan dimman sjunker ner genom marken stirrar han upp
      mot dig och vrålar.^^~Jag ska ta min hämnd! I helvetet ska vi mötas,
      du och jag. I "; style bold; print "HELVETET! IÄ! IÄ! FTHAGN";
      style roman; print "...- Aagh...~^^
      Så blir hans kropp slapp och han faller ihop. Inför dina ögon börjar
      den gamle mannen svartna och förtvina. Inom kort ser hans kropp
      ut som ett flera månader år gammalt kadaver.^^";
      if (Lamp has general) {
         print "Ett plötsligt "; style underline; print "Wumf!"; style roman;
         print " får dig att se dig om. Pölen med olja från den spruckna lyktan har
         fattat eld.  "; }
      else {
         print "Med ett ljudligt "; style bold; print "KRACK"; style
         roman; print " slår blixten ner i klocktornet på kyrkan. Ett rytande
         hörs då träbyggnaden hastigt fattar eld.  ";
         }
      if ((parent(Blue_Bottle) == player) && (location ofclass Inside))
         print "Med den andra flaskan i ett krampaktigt grepp rusar du
         ut genom porten.  ";
      if ((parent(Blue_Bottle) == Table) && (location == Inner_Chamber))
         print "Du snappar åt dig den blå flaskan från bordet och rusar ut
         genom porten.  ";
      if ((parent(Blue_Bottle) == location) && (location ofclass Inside))
      if (parent(Blue_Bottle) ~= player)
         print "Gripen av en impuls du inte kan förstå springer du fram till ",
         (the) parent(Blue_Bottle), ", plockar åt dig den, och springer sen
         iväg från kyrkan. ";
      "Du står kvar och ser på då lågorna slukar kyrkan. I ett sprakande
      dån genljuder då klocktornet ger vika och en flammande kaskad spyr uppåt
      mot himlen.^^
      Med flaskan tryckt mot bröstet styr du din dina steg ned mot sluttningen.";],
   has concealed uter def_article;

Object Sofa "gammal soffa" Inner_Chamber
   with name
      'soffa' 'soffan' 'möbel' 'möbeln' 'sittplats' 'säng' 'sängen',
   short_name_indef "gammal soffa",
   short_name_def "gamla soffan",
   parse_name [ wd adj_count noun_count;
          if (parser_action == ##TheSame) return -2;
          wd = NextWord();
          while (wd ~= 0) {
            if (Adjective_Old(wd)) {
              adj_count++;
            } else if (WordInProperty(wd, self, name)) {
              noun_count++;
            } else {
              return 0;
            }
            wd = NextWord();
          }
          if (noun_count > 0) return noun_count + adj_count;
          return 0;
          ],
   description
      "En gammal soffa, så lappad, blekt och befläckad att du inte kan uttyda
      dess ursprungliga färg. Den avger en vederstygglig stank.",
   has static scenery supporter enterable uter def_article;

Object Old_Man Sofa
   with
      is_following false,
   name
      'utmärglad' 'gamle' 'urgamle' 'uråldrige' 'utmärglade' 'gubbe' 'man' 'gubben' 'mannen' 'silas' 'wingate',
   short_name_indef [;
      if (self hasnt general)
         print "gammal man";
      else
         print "Silas";
      rtrue;],
   short_name_def [;
      if (self hasnt general)
         print "gamle mannen";
      else
         print "Silas";
      rtrue;],
   parse_name [ wd adj_count noun_count;
        if (parser_action == ##TheSame) return -2;
        wd = NextWord();
        while (wd ~= 0) {
          if (Adjective_Old(wd)) {
            adj_count++;
          } else if (WordInProperty(wd, self, name)) {
            noun_count++;
          } else {
            return 0;
          }
          wd = NextWord();
        }
        if (noun_count > 0) return noun_count + adj_count;
        return 0;
        ],
   description [;
      "En åldrad, synnerligen utmärglad man. ", (The) self, " är blek och härjad, med
      långt vildvuxet hår och och solkiga, luggslitna kläder. Men de intensivt gröna
      ögonen är vakna: i dem glöder ett skarpt intellekt och en antydan till vansinne.";],
   react_before [;
      if ((action == ##Tie) && (second == self))
         print_ret (The) self, " ryggar tillbaks då du närmar dig. ~Var inte
         en dåre,~ väser han.";],
   life [;
      Answer, Tell:
         print_ret (The) self, " skrockar lågt. ~Du tror verkligen att dina
         åsikter betyder något för mig längre?~";
      Ask:
         switch(second) {
            'kyrka', 'kyrkan', 'här', 'hem', 'hemma':
               print (The) self, " ser sig om. ~Jo... nog visste jag att
               du skulle återvända hit. Det här är ditt hem ändå... enda hemmet
               du har. Byborna hade inte välkomnat dig tidigare, det gör
               de säkerligen knappast nu. Och dessutom, flaskan kommer att hålla
               dig här. "; style underline; print "Jag"; style roman; " kommer
               att hålla dig här.~";
            'by', 'byn', 'stad', 'staden', 'bybor', 'byborna':
               print "~Dårar! Ömkliga pateter! De kan inte förstå. "; style
               underline; print "vägrar"; style roman; print " förstå. De kramar
               hellre om sin patetiska, ";
               style underline; print "trygga"; style roman; print " gud än vågar slå
               upp ögonen inför universums verkliga herrar. Du begrep.
               Du såg. Men sen... sen fick de dig att tvivla på dig
               själv. Fick dig att tvivla på ditt beslut.~";
            'beslut', 'beslutet', 'hände', 'händelse', 'resultat', 'syfte', 'syftet', 'mål', 'målet':
                 print "~Nå, att vinna odödlighet, så klart! Men, sen ändrade du dig.
               Du befriade henne. Befriade själen hennes. Men inte innan hon
               sugit åt sig för många av dina små synder, inte sant? Hon
               fick smaka evig fördömelse, för "; style underline; print
               "dina"; style roman; print " illgärningar. Och därför
               försökte du ta ditt liv. Men, där glömde du dig, eller hur?
               Glömde var "; style underline; print
               "din"; style roman; " själ tagit vägen.~";
            'hon', 'henne', 'dam', 'damen', 'kvinna', 'kvinnan', 'kärlek', 'älskade', 'oskyldig', 'oskyldiga', 'offer', 'offret':
               "~Fortfarande har jag svårt att se ett skäl för att du
               fann dig så besatt av henne. Uppenbarligen satte de andra
               byborna aldrig något värde på ert 'förhållande.'~";
            'jag', 'mig', 'själv', 'död', 'döden', 'självmord', 'ta', 'liv', 'livet':
               print (The) self, " skrattar till. ~Vad hade du väntat dig?
               När du insåg vad som hänt "; style underline; print "henne";
               style roman; print " och begrep att det var "; style underline;
               print "du"; style roman; " som bar ansvaret, då blev
               du, hur säger man nu, vansinnig? Du var alltid vek. Det fanns
               bara en sak kvar att göra, eller hur?~";
            'flaska', 'flaskor', 'flaskan', 'flaskorna':
               print "~Utsökta, inte sant? Det var "; style underline; print
               "dina"; style roman; " ord. Jag minns hur du brukade stirra in
               i dem timme efter timme. Hur du brukade försöka förstå dem...~";
            'blå', 'blåa':
               print "~Jo. Det är du. Eller, borde jag kanske säga, ";
               style underline; print "var"; style roman; print " du. Den är";
               style underline; print "min"; style roman; print " nu. Kärlet för ";
               style underline; print "min"; style roman; " synd.~";
            'grön', 'gröna':
               print "~Förstår du? Jag går säker nu... jag har överfört min synd till
               din själ. "; style underline; print "Om"; style roman; print " jag
               någonsin bestämmer mig för att befria den, då kommer jag att
               färdas till "; style underline; print "min"; style roman; print "
               belöning, medan du kommer att föras till "; style underline;
               print "mitt"; style roman; " straff.~";
            'själ', 'själar', 'själen', 'själarna':
               "~Säkra, det lovar jag dig. De är i säkerhet.~";
            'bok', 'boken', 'formelbok', 'formelboken', 'böcker', 'böckerna',
            'bibliotek', 'skrift', 'skrifter', 'skrifterna': "~Du hade aldrig
            stake nog att läsa dem förut. Varifrån kom ditt nyfunna intresse?~";
            'hund', 'hunden', 'vakthund', 'vakthunden':
               "~En användbar best. Högeligen intelligent, vet du.~";
            'man', 'mannen', 'gammal', 'gamle', 'gamling', 'han', 'honom', 'själv',
            'silas':
               if (self has general) "~Silas Wingate.~ Han skrattar till.
               ~Till er tjänst.~";
               give self general;
               give self proper;
               give self ~def_article;
               "~Vafalls? Du glömde mig? Hur kunde du glömma bort Silas? Din tjänare...
               din kollega... din... lärare?~";
             }
         print_ret (The) self, " fnittrar okontrollerat.";
      Attack:
         print_ret (The) self, " glider med lätthet undan dina utfall. Han stirrar
         på dig med klart gröna ögon. ~Redan förut var du inte min like... du är
         sannerligen inte min överman nu!~";
      ThrownAt:
         move noun to location;
         print_ret (The) self, " undviker med lätthet ", (the) noun, ". ~Ömkligt,~
         säger han.";
      Show, Give:
         print_ret (The) self, " föser bort ",(the) noun, ".";],
   daemon [d p;
      d = parent(self);
      p = location;
      if ((location == Inner_Chamber) || (p == d)) {
         self.is_following = true;}
      else {
         if (self.is_following) {
            move self to p;
            "^", (The) self, " hasar efter dig, babblande för sig själv.";}}
      rtrue;],
   has male animate def_article;

Inside Belfry "Klockstapeln"
   with description
      "Kyrkans klockstapel är högt uppe i tornet. Du klamrar dig fast vid en
      labyrint av stödbjälkar och hängverk, och närmaste fasta golv ligger
      långt under dig. Vida fönsteröppningar ger dig utsikt över den omgivande
      skogen och staden vid kullens fot. De släpper också in stormens fulla raseri
      med oförminskad styrka. Just utanför den södra fönsteröppningen skymtar du en
      trädgren från en närliggande ek.",
   before [;
      Jump:
         deadflag = 1;
         "Du kastar dig från bjälken och susar ner mot golvet därnere. Det sista
         du hör är åskans dån utifrån.";
      Climb:
         if (noun ~= Rope)
            "Du klättrar runt på bjälkarna, men du hittar inget sätt att
            ta dig vare sig vidare upp eller tillbaks ner längs tornet.";
      Drop:
         if (noun ~= Rope) {
            move noun to Steeple_Chamber;
            print_ret (The) noun, " singlar mellan bjälkarna mot golvet
            där nere.";}
         else {
            move Rope to location;
            "Repet hänger återigen fritt från klockan.";}],
   s_to Top_Of_Tree,
   out_to Top_Of_Tree,
   u_to "Det verkar inte finnas någon väg att klättra högre upp i tornet.",
   d_to [; if (Rope in location)
              return Rope;
           else
             "Det är för långt ner för att du ska kunna landa oskadd.";],
   cant_go "Du klättrar en stund längs bjälkarna, men du kan inte ta dig särskilt långt
           i den riktningen.";

Object Bell Belfry
   with name
      'klocka' 'klockan' 'kyrkklocka' 'kyrkklockan' 'massiv' 'massiva' 'tung' 'tunga',
   short_name_indef "klocka",
   short_name_def "klockan",
   description [;
      if (self hasnt general)
         "Denna massiva kyrkklocka kallade en gång bybefolkningen till gudstjänst. Nu
         tjänar den bara som hem för fåglar och gnagare. Klockans fästpunkt
         har lossnat på ena sidan, och den andra har nästan gett vika. Varför
         den inte redan lossnat och fallit av klockstapeln är för dig en gåta.";
      else
         "Klockan ligger halvt begravd i gyttjan. Den har bucklats till och
         spruckit av fallet till den grad att den aldrig kommer ringa igen.";],
   describe [;
      if (self hasnt general) {
         print "^En massiv kyrkklocka hänger här från ett skadat fäste. ";
         print "Ett tungt rep är fastgjort vid klockan och ";
         if ((Rope.is_tied_to ~= 0) && (Rope.is_tied_to in location))
            print "bundet runt ", (the) Rope.is_tied_to, ".";
         else
            print "löper mellan bjälkarna ner mot rummet därunder.";
         new_line; rtrue;}
      else
         "^En sprucken klocka ligger halvt begraven i gyttjan.";],
   before [;
      Take:
         "Klockan är alldeles för tung för att flyttas.";
      Push, Pull:
         if (Rope.is_tied_to == Steeple_Door) {
            Achieved(4);
            give self general;
            give Steeple_Door open ~openable;
            move Bell to Front_Church;
            move Rope to Limbo;
            Rope.is_tied_to = 0;
            give Rope absent;
            "Du föser till klockan med hela din tyngd. Under ett par ögonblick
            lyckas fästet stå emot. Sen hörs ett högljutt krasande, och
            kyrkklockan bryts loss från bjälken och tumlar ut ur tornet. Du
            hör ljudet av trä som knäcks och kastar dig hastigt åt sidan medan de
            splittrade resterna av dörren slits förbi dig och slungas ut genom
            fönstret. Ljudet av sönderslagen metall genljuder utifrån ett
            ögonblick därpå."; }
         else {
            if (self hasnt general)
               "En ordentlig knuff skulle förmodligen kunna få klockan att lossna
               av klockstapeln. Det förstås, om det skulle hända skulle du nog
               aldrig kunna få upp den hit igen, så du beslutar att låta det vara
               för tillfället.";
            else
               "Klockan är för djupt begravd i gyttjan för att flyttas.";}
      Ring:
         if (self hasnt general)
            "Kyrkklockan ger ifrån sig ett dämpat ~KLÅNG~.";
         else
            "Den här klockan kommer aldrig ringa igen.";],
   has static uter;

Object Rope
   with
      is_tied_to 0,
   short_name_indef "tungt rep",
   short_name_def "tunga repet",
   name
      'tung' 'tungt' 'tunga' 'slitet' 'slitna' 'fransigt' 'fransiga' 'hängande' 'bastant' 'rep' 'repet' 'tross' 'trossen',
   describe [;
      if (location == Steeple_Chamber) {
         print "^Ett fransigt men tungt rep hänger ner från någonstans därovan.";
         if (self.is_tied_to ~= 0) {
            if (self.is_tied_to ~= Steeple_Door)
               print " Du ser ", (a) self.is_tied_to, " som hänger från repets ände.";
            else
               print " Repet är fastbundet i dörrens handtag.";}
         new_line;}
      rtrue;],
   description [;
      print "Ett stycke fransigt men tungt rep.";
      if (self.is_tied_to ~= 0)
         print " ", (The) self.is_tied_to, " är bundet vid repet.";
      new_line; rtrue;],
   before [;
      Tie:
         if (second == player)
            "Tro mig, självmord är *inte* svaret här...";
         if (second == self)
            "Övar du på knopar?";
         if (second == Railing_Top)
            "Du drar lite i repet, men det når inte ner till luckan.";
         if (second == Bell)
            "Repet sitter redan ordentligt fast i kyrkklockan.";
         self.is_tied_to = second;
         if (parent(second) ~= player) move Rope to parent(second);
         "Du binder fast repet hårt i ", (the) second, ".";
      UnTie:
         if (self.is_tied_to == 0)
            "Men repet är ju inte fastbundet i något!";
         if (second ~= 0) {
            if (second == Bell)
               "Du kan inte låssa repet där det är fastgjort i klockan.";
            if (second ~= self.is_tied_to)
               "Men repet är inte bundet till ", (the) second, ".";}
         print "Du knyter loss repet från ", (the) self.is_tied_to, ".";
         self.is_tied_to = 0;
         new_line; rtrue;
      Drop:
         if ((self.is_tied_to ~= nothing) && (parent(self.is_tied_to) == player))
            "Du låter repet falla, men det sitter fortfarande fast i ",
            (the) self.is_tied_to, " som du håller och faller inte
            särskilt långt.";
      Climb:
         <<Go self>>;
      Pull:
         if (location == Steeple_Chamber)
            "Du kan höra ett metalliskt <KLÅNG> någonstans ovanifrån.";
         else
            "Klockan rör sig lätt i sitt fäste och ger ifrån sig ett
            metalliskt <KLÅNG>.";],
   react_before [;
      if ((action == ##Go) && (self notin location)) {
         if (self in player) {
            "Repet sitter ordentligt fast och du kan inte gå särskilt långt
            medan du håller fast i det.";}
         if (self.is_tied_to in player) {
            print_ret (The) self.is_tied_to, ", vilket du håller, är bundet
            i repet och du kan därför inte röra dig särskilt långt.";}}
      ;],
   react_after [;
      if (noun == self.is_tied_to) {
         if ((action == ##Drop) || (action == ##Insert) ||
             (action == ##ThrowAt) || (action == ##PutOn) ||
             (action == ##PutUnder)) {
            move self to parent(self.is_tied_to);
            if (location == Belfry) move self to Belfry;}
         if ((action == ##Take) || (action == ##Remove)) {
            move self to player;}}],
   door_to [; if (location == Steeple_Chamber) return Belfry;
              return Steeple_Chamber;],
   door_dir [; if (location == Steeple_Chamber) return u_to;
               return d_to;],
   found_in Steeple_Chamber Belfry,
   has door ~openable open neuter def_article;

! Se till att vi kan se dem i Steeple_Chamber, men inte interagera med dem.
Object Beams Belfry
   with name
      'stöd' 'stöden' 'bjälke' 'bjälken' 'bjälkar' 'bjälkarna' 'trä' 'träbjälke' 'träbjälkar' 'träbjälkarna',
   short_name_indef "bjälkar",
   short_name_def "bjälkarna",
   description [;
      print "Grovt utmejslade träbjälkar.";
      if (location == Belfry) " Ett tjockt lager smuts och föroreningar från fåglar och
      andra djur ger vanskligt fotfäste.";
      ""; ],
   found_in Steeple_Chamber Belfry,
   has pluralname static scenery;

! Kan vi ta dem? Hoppas inte.
Object Town
   with name
      'stad' 'staden' 'by' 'byn' 'småstad' 'småstaden' 'fot' 'foten' 'kulle' 'kullen' 'ljus' 'ljuspunkt' 'liten' 'lilla' 'samhälle' 'samhället',
   short_name_indef "stad",
   short_name_def "staden",
   description
      "Den lilla staden kurar vid kullens fot. Enstaka ljuspunkter flackar
      trotsigt genom stormen, men på det stora hela verkar den tyst, som om
      de boende valt att vänta ut stormens vrede inomhus.",
   found_in Top_Of_Tree Belfry,
   has scenery uter;

Object Debris
   with name
      'skräp' 'skräpet' 'bråte' 'bråtet' 'smuts' 'smutset' 'smutsen' 'smutsigt' 'smutsiga' 'rester' 'resterna'
      'lort' 'lorten' 'pinnar' 'pinnarna' 'löv' 'löven' 'papper' 'pappren' 'bitar' 'bitarna',
   short_name_indef "bråte",
   short_name_def "bråtet",
   description [;
      if (location == East_church) "Blandade pinnar, löv och bråte täcker grusplanen.";
      "Pinnar, löv, obestämbart skräp och pappersrester ligger utströdda över
      golvet. Några av dem ser ut att ha varit här i åratal och ingen av dem
      är användbara."; ],
   before [;
      Take:
         "Du har inget bruk för något av skräpet.";],
   found_in [; if (location ofclass Inside) rtrue; if (location == East_Church) rtrue;],
   has scenery neuter;

Include "../awakening/subsections/hints.inf";

!*******************************************************************************
!*** And awaaay we go!
!*******************************************************************************
[Initialise;
   location = Grave;

   StartDaemon(Dog);
   StartDaemon(Old_Man);

   "^^Du minns mörker. Och köld. Kyla och mörker. Och tystnaden. De minnena är
   allt du har kvar. Minnena, och förnimmelsen av något som du aldrig vetat var
   ditt, och som är borta.

   Plötsligt klyvs tomheten av ett bländvitt ljussken och en dundrande knall.
   Uppvaknandet övergår i stormens rytande, till en syndaflod av regn och lera.^";];

[ChooseObjects obj code prty;
   if (code < 2)
      {if ((obj ofclass Fake_Object) || (obj has scenery))
         return 2;
      else
         rfalse;}
   else
      {prty = 2;
      if (obj has scenery) prty = prty - 1;
      if (parent(player) == parent(obj)) prty = prty + 1;
      if (parent(obj) == player) prty = prty + 2;
      if ((action_to_be == ##Take) && (obj hasnt static)) {
         prty = prty + 1;
      }
      if ((action_to_be == ##SwitchOn) && (parent(player) == obj))
         prty = prty + 1;
      return prty;}];

[PrintTaskName achievement;
   print "för att du ";
   switch(achievement)
      {0: "kom förbi vakthunden";
       1: "besökte tornrummet";
       2: "besökte det förseglade rummet";
       3: "läste liggaren";
       4: "öppnade dörren";
       5: "uppdagade sanningen";
       6: "utkrävde din hämnd";}];

Include "SwedishG";

[ReadSub;
   <<Examine noun>>;];

[KnockSub;
   "Du ser inte meningen med att knacka på ", (the) noun, ".";];

[PutUnderSub;
   "Du vet inte hur du skulle få ", (the) noun, " att hamna under ", (the) second , ".";];

[UnTieSub;
   print_ret (The) noun, " sitter inte fast i något.";];

[RingSub;
   "Du vet inte hur man ringer i ", (the) noun, ".";];

Verb "klappa" = "gnid";
Verb "gosa" = "gnid";
Verb "sparka" = "attackera";
Verb "luta" = "ställ";

Extend "läs" first
     * noun                          -> Read;

Extend "ställ" first
     * noun "under"/"nedanför"/"invid"/"emot"/"mot" noun
                                     -> PutUnder;

Extend "sätt" first
     * noun "under"/"nedanför"/"invid"/"emot"/"mot" noun
                                     -> PutUnder;

Extend "gnid"
     * noun "med" noun              -> Rub reverse
     * noun "på"/"i"/"över"/"mot" noun    -> Rub
     * "in" noun "på"/"i"/"över"/"mot" noun    -> Rub;

Verb "knacka" "bulta"
     * noun                          -> Knock
     * "på" noun                     -> Knock;

Verb "lossa" "lösgör"
     * noun                          -> UnTie
     * noun "från" noun              -> UnTie;

Verb "ring"
     * noun                          -> Ring;

Verb "xyzzy"
     *                               -> Xyzzy;

[XyzzySub;
   "Ett ord kommer till minnes, och du ropar ut det... ~XYZZY!~ Stormen ger
   svar i ett rytande och åskan dundrar. Det är ett kraftord. En gång, det
   vet du, var många sådana ord dina. Nu är detta det enda som återstår.";];
